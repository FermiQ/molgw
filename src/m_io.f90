!=========================================================================
! This file is part of MOLGW.
! Author: Fabien Bruneval
!
! This file contains
! the procedures for input and outputs
!
!=========================================================================
module m_io
#if defined(FORTRAN2008)
 use,intrinsic :: iso_fortran_env, only: compiler_version,compiler_options
#endif
 use m_definitions
 use m_mpi
 use m_timing
 use m_memory
 use m_warning,only: issue_warning
 use m_string_tools,only: orbital_momentum_name
 use m_libint_tools,only: libint_init
 use m_libxc_tools,only: xc_version
 use m_linear_algebra,only: determinant_3x3_matrix
 use m_inputparam
 use m_hamiltonian_tools,only: get_number_occupied_states
 use m_atoms
 use m_basis_set
 use m_dft_grid,only: calculate_basis_functions_r
 use m_cart_to_pure
 use m_tddft_variables
 use m_eri,only: npair




contains


!=========================================================================
!
! Cleanly exit from the code, with all the memory statements, warnings, and timings
!
!=========================================================================
subroutine this_is_the_end()
 implicit none

!=====
#if defined(_OPENMP)
 integer,external  :: OMP_get_max_threads
#endif
!=====

 call total_memory_statement()

 call output_timing()

 call output_all_warnings()

 if( print_yaml_ .AND. is_iomaster ) then
   write(unit_yaml,'(/,a)')  'run:'
   write(unit_yaml,'(4x,a,1x,i6)')  'mpi tasks:  ',nproc_world
#if defined(_OPENMP)
   write(unit_yaml,'(4x,a,1x,i6)')  'omp threads:',OMP_get_max_threads()
#else
   write(unit_yaml,'(4x,a,1x,i6)')  'omp threads:',1
#endif

   write(unit_yaml,'(4x,a)') 'timing:'
   write(unit_yaml,'(8x,a)')           'unit: s'
   write(unit_yaml,'(8x,a,1x,es18.8)') 'total:  ',get_timing(timing_total)
   write(unit_yaml,'(8x,a,1x,es18.8)') 'prescf: ',get_timing(timing_prescf)
   write(unit_yaml,'(8x,a,1x,es18.8)') 'scf:    ',get_timing(timing_scf)
   write(unit_yaml,'(8x,a,1x,es18.8)') 'postscf:',get_timing(timing_postscf)
   write(unit_yaml,'(4x,a)') 'memory:'
   write(unit_yaml,'(8x,a)')           'unit: Gb'
   write(unit_yaml,'(8x,a,1x,es18.8)') 'peak:   ',get_peak_memory()

   write(unit_yaml,'(a)') '...'
   close(unit_yaml)
 endif

 write(stdout,'(/,1x,a,/)') 'This is the end'

 call finish_mpi()

 stop


end subroutine this_is_the_end


!=========================================================================
subroutine header()
 implicit none

!=====
#if defined(_OPENMP)
 integer,external  :: OMP_get_max_threads
#endif
 character(len=40)   :: git_sha
 integer             :: values(8)
#if defined(FORTRAN2008)
 integer             :: nchar,kchar,lchar
#endif
 character(len=1024) :: chartmp
!=====
! variables used to call C
 integer(C_INT)      :: ammax
 logical(C_BOOL)     :: has_onebody,has_gradient
 integer(C_INT)      :: libxc_version(3)
!=====

! Here we call the fortran code that was generated by the python script
! Any new variable should be added through the python script
#include "git_sha.f90"

!=====

 write(stdout,'(1x,70("="))')
 write(stdout,'(/,/,12x,a,/)') 'Welcome to the fascinating world of MOLGW'
 write(stdout,'(24x,a)')       'version 2.B'
 write(stdout,'(/,/,1x,70("="))')

 write(stdout,'(/,a,a,/)') ' MOLGW commit git SHA: ',git_sha
#if defined(FORTRAN2008)
 write(stdout,'(1x,a,a)')    'compiled with ',compiler_version()
 write(stdout,'(1x,a)')      'with options: '
 chartmp = compiler_options()
 nchar = LEN(TRIM(chartmp))
 kchar = 1
 lchar = 0
 do
   lchar = SCAN(chartmp(kchar:nchar),' ')
   if( lchar == 0 ) exit
   write(stdout,'(6x,a,a)') 'FCOPT  ',chartmp(kchar:kchar+lchar-1)
   kchar = kchar + lchar
 enddo
 write(stdout,*)
#endif


 call date_and_time(VALUES=values)

 write(stdout,'(a,i2.2,a,i2.2,a,i4.4)') ' Today is ',values(2),'/',values(3),'/',values(1)
 write(stdout,'(a,i2.2,a,i2.2)')        ' It is now ',values(5),':',values(6)
 select case(values(5))
 case(03,04,05,06,07)
   write(stdout,*) 'And it is too early to work. Go back to sleep'
 case(22,23,00,01,02)
   write(stdout,*) 'And it is too late to work. Go to bed and have a sleep'
 case(12,13)
   write(stdout,*) 'Go and get some good food'
 case(17)
   write(stdout,*) 'Dont forget to go and get the kids'
 case default
   write(stdout,*) 'And it is perfect time to work'
 end select


 write(stdout,'(/,1x,a)') 'Preprocessing and runtime options:'
#if defined(HAVE_LIBXC)
 write(stdout,*) 'Running with LIBXC'
 call xc_version(libxc_version(1),libxc_version(2),libxc_version(3))
 write(chartmp,'(i2,a,i2,a,i2)') libxc_version(1),'.',libxc_version(2),'.',libxc_version(3)
 write(stdout,'(5x,a,a)') 'LIBXC version: ',TRIM(chartmp)
#endif


 ! Parallelization details
#if defined(_OPENMP)
 write(stdout,'(1x,a,i4)') 'Running with OPENMP parallelization activated with max threads count: ',OMP_get_max_threads()
#endif
#if defined(HAVE_MPI) && defined(HAVE_SCALAPACK)
 write(stdout,*) 'Running with MPI'
 write(stdout,*) 'Running with SCALAPACK'
#endif
#if defined(HAVE_MPI) && !defined(HAVE_SCALAPACK)
 call die('Code compiled with SCALAPACK, but without MPI. This is not permitted')
#endif
#if !defined(HAVE_MPI) && defined(HAVE_SCALAPACK)
 call die('Code compiled with MPI, but without SCALAPACK. This is not permitted')
#endif

 ! LIBINT details
 call libint_init(ammax,has_onebody,has_gradient)
 write(stdout,'(1x,a)')         'Running with LIBINT (to calculate the Coulomb integrals)'
 write(stdout,'(6x,a,i5,3x,a)') 'max angular momentum handled by your LIBINT compilation: ', &
                                ammax,orbital_momentum_name(ammax)
 call set_molgw_lmax(ammax)

#if defined(HAVE_LIBINT_GRADIENTS) && !defined(HAVE_LIBINT_ONEBODY)
 call die('MOLGW gradients requires both compilations HAVE_LIBINT_GRADIENTS and HAVE_LIBINT_ONEBODY')
#endif

#if defined(HAVE_LIBINT_ONEBODY)
 if( .NOT. has_onebody ) &
   call die('MOLGW compiled with LIBINT one-body terms, however the LIBINT compilation does not calculate the one-body terms')
 write(stdout,'(1x,a)')  'Running with external LIBINT calculation of the one-body operators (faster)'
#else
 write(stdout,'(1x,a)')  'Running with internal calculation of the one-body operators (slower)'
#endif

#if defined(HAVE_LIBINT_GRADIENTS)
 if( .NOT. has_gradient ) &
   call die('LIBINT compilation does not have the first derivative')
 write(stdout,'(1x,a)') 'Running with external LIBINT calculation of the gradients of the integrals'
#endif
 write(stdout,*)
 write(stdout,*)


end subroutine header


!=========================================================================
subroutine dump_out_matrix(print_matrix,title,n,nspin,matrix)
 implicit none
 logical,intent(in)          :: print_matrix
 character(len=*),intent(in) :: title
 integer,intent(in)          :: n,nspin
 real(dp),intent(in)         :: matrix(n,n,nspin)
!=====
 integer,parameter :: MAXSIZE=50
!=====
 integer :: i,ispin
!=====

 if( .NOT. print_matrix ) return

 write(stdout,'(/,1x,a)') TRIM(title)

 do ispin=1,nspin
   if(nspin==2) then
     write(stdout,'(a,i1)') ' spin polarization # ',ispin
   endif
   do i=1,MIN(n,MAXSIZE)
     write(stdout,'(1x,i3,100(1x,f12.5))') i,matrix(i,1:MIN(n,MAXSIZE),ispin)
   enddo
   write(stdout,*)
 enddo
 write(stdout,*)

end subroutine dump_out_matrix


!=========================================================================
subroutine mulliken_pdos(nstate,basis,s_matrix,c_matrix,occupation,energy)
 implicit none
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: s_matrix(basis%nbf,basis%nbf)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
 real(dp),intent(in)        :: occupation(nstate,nspin),energy(nstate,nspin)
!=====
 integer                    :: ibf,li,ibf1,ibf2,ishell
 integer                    :: natom1,natom2,istate,ispin
 logical                    :: file_exists
 integer                    :: pdosfile
 real(dp)                   :: proj_state_i(0:basis%ammax),proj_charge
 real(dp)                   :: cs_vector_i(basis%nbf)
 integer                    :: iatom_ibf(basis%nbf)
 integer                    :: li_ibf(basis%nbf)
 real(dp)                   :: proj_atom(natom_basis)
 integer                    :: ielement,iemax,iatom_basis
 integer                    :: atom2element(natom_basis)
 character(len=4)           :: char4
 character(len=2)           :: char2
 real(dp),allocatable       :: proj_element(:,:)
 integer,allocatable        :: element_list(:)
 real(dp)                   :: weight
 integer,parameter          :: lmax = 2
!=====

 if( .NOT. is_iomaster ) return


 write(stdout,*)
 write(stdout,*) 'Projecting wavefunctions on selected atoms'
 inquire(file='manual_pdos',exist=file_exists)
 if(file_exists) then
   write(stdout,*) 'Opening file:','manual_pdos'
   open(newunit=pdosfile,file='manual_pdos',status='old')
   read(pdosfile,*) natom1,natom2
   close(pdosfile)
 else
   natom1=1
   natom2=1
 endif
 write(stdout,'(1x,a,i5,2x,i5)') 'Range of atoms considered: ',natom1,natom2

 if( print_yaml_ ) write(unit_yaml,'(/,a)') 'mulliken projections:'

 do ishell=1,basis%nshell
   ibf1    = basis%shell(ishell)%istart
   ibf2    = basis%shell(ishell)%iend

   iatom_ibf(ibf1:ibf2) = basis%shell(ishell)%iatom
   li_ibf(ibf1:ibf2)    = basis%shell(ishell)%am
 enddo

 ! Find the unique elements in the system
 iemax = 0
 do ielement=1,nelement_max
   if( ANY(zbasis(:) == ielement) ) then
     iemax = iemax + 1
   endif
   do iatom_basis=1,natom_basis
     if( zbasis(iatom_basis) == ielement ) atom2element(iatom_basis) = iemax
   enddo
 enddo
 allocate(proj_element(0:lmax+1,iemax),element_list(iemax))
 iemax = 0
 do ielement=1,nelement_max
   if( ANY(zbasis(:) == ielement) ) then
     iemax = iemax + 1
     element_list(iemax) = ielement
   endif
 enddo

 write(stdout,'(1x,a)') '==========================================='
 write(stdout,'(1x,a)') 'spin state  energy(eV)  Mulliken proj. total        proj s         proj p      proj d ... '

 proj_charge = 0.0_dp

 do ispin=1,nspin

   if( print_yaml_ ) write(unit_yaml,'(4x,a,i2,a)') 'spin channel',ispin,':'

   do istate=1,nstate
     proj_state_i(:) = 0.0_dp
     proj_element(:,:) = 0.0_dp
     write(char4,'(i4)') istate

     cs_vector_i(:) = MATMUL(  s_matrix(:,:) , c_matrix(:,istate,ispin) )

     do ibf=1,basis%nbf
       if( iatom_ibf(ibf) >= natom1 .AND. iatom_ibf(ibf) <= natom2 ) then
         li = li_ibf(ibf)
         proj_state_i(li) = proj_state_i(li) + c_matrix(ibf,istate,ispin) * cs_vector_i(ibf)
       endif
       weight = c_matrix(ibf,istate,ispin) * cs_vector_i(ibf)
       proj_element(MIN(li_ibf(ibf),lmax+1),atom2element(iatom_ibf(ibf))) = proj_element(MIN(li_ibf(ibf),lmax+1),atom2element(iatom_ibf(ibf))) + weight
     enddo
     proj_charge = proj_charge + occupation(istate,ispin) * SUM(proj_state_i(:))
     if( print_yaml_) then
       write(unit_yaml,'(8x,a4,a)') ADJUSTL(char4),':'
       do ielement=1,iemax
         write(unit_yaml,'(12x,a2,a)') ADJUSTL(element_name(REAL(element_list(ielement),dp))),':'
         do li=0,lmax
           write(char2,'(i2)') li
           write(unit_yaml,'(16x,a2,3x,a,1x,es18.8)') ADJUSTL(char2),':',proj_element(li,ielement)
         enddo
         write(unit_yaml,'(16x,a,1x,es18.8)') 'total:',SUM(proj_element(:,ielement))
       enddo
     endif

     write(stdout,'(i3,1x,i5,1x,20(f16.6,4x))') ispin,istate,energy(istate,ispin) * Ha_eV,&
          SUM(proj_state_i(:)),proj_state_i(:)
   enddo
 enddo
 write(stdout,'(1x,a)') '==========================================='
 write(stdout,'(1x,a,f12.6)') 'Total Mulliken charge: ',proj_charge


end subroutine mulliken_pdos


!=========================================================================
subroutine plot_wfn(nstate,basis,c_matrix)
 implicit none
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
!=====
 integer,parameter          :: nr=2000
 real(dp),parameter         :: length=10.0_dp
 integer                    :: ir
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 real(dp),allocatable       :: phi(:,:),phase(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: wfrfile
!=====

 if( .NOT. is_iomaster ) return


 write(stdout,'(/,1x,a)') 'Plotting some selected wavefunctions'
 inquire(file='manual_plotwfn',exist=file_exists)
 if(file_exists) then
   open(newunit=wfrfile,file='manual_plotwfn',status='old')
   read(wfrfile,*) istate1,istate2
   read(wfrfile,*) u(:)
   read(wfrfile,*) a(:)
   close(wfrfile)
 else
   istate1=1
   istate2=2
   u(:)=0.0_dp
   u(1)=1.0_dp
   a(:)=0.0_dp
 endif
 u(:) = u(:) / SQRT(SUM(u(:)**2))
 allocate(phase(istate1:istate2,nspin),phi(istate1:istate2,nspin))
 write(stdout,'(a,2(2x,i4))')   ' states:   ',istate1,istate2
 write(stdout,'(a,3(2x,f8.3))') ' direction:',u(:)
 write(stdout,'(a,3(2x,f8.3))') ' origin:   ',a(:)

 xxmin = MINVAL( u(1)*xbasis(1,:) + u(2)*xbasis(2,:) + u(3)*xbasis(3,:) ) - length
 xxmax = MAXVAL( u(1)*xbasis(1,:) + u(2)*xbasis(2,:) + u(3)*xbasis(3,:) ) + length

 phase(:,:)=1.0_dp

 do ir=1,nr
   rr(:) = ( xxmin + (ir-1)*(xxmax-xxmin)/REAL(nr-1,dp) ) * u(:) + a(:)


   call calculate_basis_functions_r(basis,rr,basis_function_r)

   do ispin=1,nspin
     phi(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,istate1:istate2,ispin) )
   enddo

   !
   ! turn the wfns so that they are all positive at a given point
   if(ir==1) then
     do ispin=1,nspin
       do istate=istate1,istate2
         if( phi(istate,ispin) < 0.0_dp ) phase(istate,ispin) = -1.0_dp
       enddo
     enddo
   endif

   write(101,'(50(e16.8,2x))') DOT_PRODUCT(rr(:),u(:)),phi(:,:)*phase(:,:)
   write(102,'(50(e16.8,2x))') DOT_PRODUCT(rr(:),u(:)),phi(:,:)**2

 enddo

 deallocate(phase,phi)

end subroutine plot_wfn


!=========================================================================
subroutine plot_rho(nstate,basis,occupation,c_matrix)
 implicit none
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
!=====
 integer,parameter          :: nr=5000
 real(dp),parameter         :: length=4.0_dp
 integer                    :: ir
 integer                    :: ispin
 real(dp)                   :: rr(3)
 real(dp),allocatable       :: phi(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: rhorfile
!=====

 if( .NOT. is_iomaster ) return

 write(stdout,'(/,1x,a)') 'Plotting the density'


 inquire(file='manual_plotrho',exist=file_exists)
 if(file_exists) then
   open(newunit=rhorfile,file='manual_plotrho',status='old')
   read(rhorfile,*) u(:)
   read(rhorfile,*) a(:)
   close(rhorfile)
 else
   u(:)=0.0_dp
   u(1)=1.0_dp
   a(:)=0.0_dp
 endif
 u(:) = u(:) / NORM2(u)
 allocate(phi(nstate,nspin))
 write(stdout,'(a,3(2x,f8.3))') ' direction:',u(:)
 write(stdout,'(a,3(2x,f8.3))') ' origin:   ',a(:)

 xxmin = MINVAL( u(1)*xbasis(1,:) + u(2)*xbasis(2,:) + u(3)*xbasis(3,:) ) - length
 xxmax = MAXVAL( u(1)*xbasis(1,:) + u(2)*xbasis(2,:) + u(3)*xbasis(3,:) ) + length

 do ir=1,nr
   rr(:) = ( xxmin + (ir-1)*(xxmax-xxmin)/REAL(nr-1,dp) ) * u(:) + a(:)


   call calculate_basis_functions_r(basis,rr,basis_function_r)

   do ispin=1,nspin
     phi(:,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,:,ispin) )
   enddo

   write(103,'(50(e16.8,2x))') DOT_PRODUCT(rr(:),u(:)),SUM( phi(:,:)**2 * occupation(:,:) )

   write(104,'(50(e16.8,2x))') NORM2(rr(:)),SUM( phi(:,:)**2 * occupation(:,:) ) * 4.0_dp * pi * NORM2(rr(:))**2
!   write(105,'(50(e16.8,2x))') NORM2(rr(:)),SUM( phi(1,:)**2 * occupation(1,:) ) * 4.0_dp * pi * NORM2(rr(:))**2
!   write(106,'(50(e16.8,2x))') NORM2(rr(:)),SUM( phi(2:,:)**2 * occupation(2:,:) ) * 4.0_dp * pi * NORM2(rr(:))**2

 enddo

 deallocate(phi)

end subroutine plot_rho


!=========================================================================
subroutine plot_rho_list(nstate,basis,occupation,c_matrix)
 implicit none
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
!=====
 integer                    :: ispin
 real(dp)                   :: rr(3)
 real(dp),allocatable       :: phi(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: rhorfile
 integer                    :: ix,iy,iz
 integer,parameter          :: nx=75 ! 87
 integer,parameter          :: ny=75 ! 91
 integer,parameter          :: nz=90 ! 65
 real(dp),parameter         :: dx = 0.174913 ! 0.204034
 real(dp)                   :: rr0(3)
 integer                    :: unitfile
!=====

 if( .NOT. is_iomaster ) return

 write(stdout,'(/,1x,a)') 'Plotting the density'


 inquire(file='manual_plotrho',exist=file_exists)
 if(file_exists) then
   open(newunit=rhorfile,file='manual_plotrho',status='old')
   close(rhorfile)
 else
 endif
 allocate(phi(nstate,nspin))

 rr0(1) = -6.512752 ! -8.790885
 rr0(2) = -6.512752 ! -9.143313
 rr0(3) = -7.775444 ! -6.512752

 open(newunit=unitfile,file='rho.dat',action='WRITE')
 do ix=1,nx
 do iy=1,ny
 do iz=1,nz
   rr(1) = ix-1
   rr(2) = iy-1
   rr(3) = iz-1
   rr(:) = rr0(:) + rr(:) * dx


   call calculate_basis_functions_r(basis,rr,basis_function_r)

   do ispin=1,nspin
     phi(:,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,:,ispin) )
   enddo

   write(unitfile,'(1x,e16.8)') SUM( phi(3:,:)**2 * occupation(3:,:) )

 enddo
 enddo
 enddo
 close(unitfile)

 deallocate(phi)

end subroutine plot_rho_list


!=========================================================================
subroutine plot_cube_wfn(rootname,nstate,basis,occupation,c_matrix)
 implicit none
 character(len=*)           :: rootname
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
!=====
 integer                    :: nx,ny,nz
 real(dp),parameter         :: length=3.499470_dp
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 real(dp),allocatable       :: phi(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ix,iy,iz,iatom
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: ocuberho(nspin)
 character(len=200)         :: file_name
 integer                    :: icubefile
!=====

 if( .NOT. is_iomaster ) return

 write(stdout,'(/,1x,a)') 'Plotting some selected wavefunctions in a cube file'


 inquire(file='manual_cubewfn',exist=file_exists)
 if(file_exists) then
   open(newunit=icubefile,file='manual_cubewfn',status='old')
   read(icubefile,*) istate1,istate2
   read(icubefile,*) nx,ny,nz
   close(icubefile)
 else
   istate1=1
   istate2=2
   nx=40
   ny=40
   nz=40
 endif
 istate1 = MAX(istate1,1)
 istate2 = MIN(istate2,nstate)

 allocate(phi(istate1:istate2,nspin))
 write(stdout,'(a,2(2x,i4))')   ' states:   ',istate1,istate2

 xmin =MIN(MINVAL( xatom(1,:) ),MINVAL( xbasis(1,:) )) - length
 xmax =MAX(MAXVAL( xatom(1,:) ),MAXVAL( xbasis(1,:) )) + length
 ymin =MIN(MINVAL( xatom(2,:) ),MINVAL( xbasis(2,:) )) - length
 ymax =MAX(MAXVAL( xatom(2,:) ),MAXVAL( xbasis(2,:) )) + length
 zmin =MIN(MINVAL( xatom(3,:) ),MINVAL( xbasis(3,:) )) - length
 zmax =MAX(MAXVAL( xatom(3,:) ),MAXVAL( xbasis(3,:) )) + length
 dx = (xmax-xmin)/REAL(nx,dp)
 dy = (ymax-ymin)/REAL(ny,dp)
 dz = (zmax-zmin)/REAL(nz,dp)
! xmin = -15.001591d0
! ymin = -15.001591d0
! zmin = -17.037892d0
! dx = 0.262502_dp
! dy = 0.262502_dp
! dz = 0.262502_dp

 allocate(ocubefile(istate1:istate2,nspin))

 do istate=istate1,istate2
   do ispin=1,nspin
     write(file_name,'(a,i3.3,a,i1,a)') 'wfn_'//TRIM(rootname)//'_',istate,'_',ispin,'.cube'
     open(newunit=ocubefile(istate,ispin),file=file_name)
     write(ocubefile(istate,ispin),'(a)') 'cube file generated from MOLGW'
     write(ocubefile(istate,ispin),'(a,i4)') 'wavefunction ',istate1
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') natom,xmin,ymin,zmin
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') nx,dx,0.,0.
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') ny,0.,dy,0.
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') nz,0.,0.,dz
     do iatom=1,natom
       write(ocubefile(istate,ispin),'(i6,4(2x,f12.6))') NINT(zatom(iatom)),0.0,xatom(:,iatom)
     enddo
   enddo
 enddo

 !
 ! check whether istate1:istate2 spans all the occupied states
 if( ALL( occupation(istate2+1:nstate,:) < 1.0e-2_dp ) ) then
   do ispin=1,nspin
     write(file_name,'(a,i1,a)') 'rho_'//TRIM(rootname)//'_',ispin,'.cube'
     open(newunit=ocuberho(ispin),file=file_name)
     write(ocuberho(ispin),'(a)') 'cube file generated from MOLGW'
     write(ocuberho(ispin),'(a,i4)') 'density for spin ',ispin
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') natom,xmin,ymin,zmin
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') nx,dx,0.,0.
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') ny,0.,dy,0.
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') nz,0.,0.,dz
     do iatom=1,natom
       write(ocuberho(ispin),'(i6,4(2x,f12.6))') NINT(zatom(iatom)),0.0,xatom(:,iatom)
     enddo
   enddo

   do ix=1,nx
     rr(1) = xmin + (ix-1)*dx
     do iy=1,ny
       rr(2) = ymin + (iy-1)*dy
       do iz=1,nz
         rr(3) = zmin + (iz-1)*dz


         call calculate_basis_functions_r(basis,rr,basis_function_r)

         do ispin=1,nspin
           phi(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,istate1:istate2,ispin) )
         enddo

         do ispin=1,nspin
           write(ocuberho(ispin),'(50(e16.8,2x))') SUM( phi(:,ispin)**2 * occupation(istate1:istate2,ispin) )
         enddo


         do istate=istate1,istate2
           do ispin=1,nspin
             write(ocubefile(istate,ispin),'(50(e16.8,2x))') phi(istate,ispin)
           enddo
         enddo

       enddo
     enddo
   enddo


   do ispin=1,nspin
     do istate=istate1,istate2
       close(ocubefile(istate,ispin))
     enddo
     close(ocuberho(ispin))
   enddo
 endif

 deallocate(phi)
 deallocate(ocubefile)

end subroutine plot_cube_wfn

!=========================================================================
subroutine plot_rho_traj_bunch(nstate,nocc_dim,basis,occupation,c_matrix,num,time_cur)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
 integer                    :: num
 real(dp),intent(in)        :: time_cur
!=====
 integer                    :: nr=1000,nh=100
 integer                    :: gt
 integer                    :: nocc(2),nocc_max
 real(dp),parameter         :: length=6.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi(:,:)
 real(dp)                   :: point_a(3),point_b(3),point_c(3),u(3)
 real(dp)                   :: a_cur(3), b_cur(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ir,ih,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: line_rho(nspin)
 character(len=200)         :: file_name
 integer                    :: linefile
 integer                    :: i_max_atom
 real(dp)                   :: vec_length
 real(dp)                   :: integral, deltar
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_line_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting electronic density along the projectile trajectory for several impact parameters'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_rho_traj_bunch subroutine')
   endif
 enddo

 inquire(file='manual_dens_traj',exist=file_exists)
 if(file_exists) then
   open(newunit=linefile,file='manual_dens_traj_tddft',status='old')
   read(linefile,*) point_a(:)
   read(linefile,*) point_b(:)
   read(linefile,*) point_c(:)
   read(linefile,*) nr
   read(linefile,*) nh

   close(linefile)
 else
   point_a = (/ 0.0_dp,  0.0_dp, -10.0_dp  /)
   point_b = (/ 0.0_dp,  0.0_dp,  10.0_dp  /)
   point_c = (/ 3.49_dp, 0.0_dp, -10.0_dp  /)
   call issue_warning('plot_rho_traj_bunch: manual_dens_traj_tddft file was not found')
 endif
! point_b(:) = point_b(:) / bohr_A
! point_a(:) = point_a(:) / bohr_A
! In analogy with cube file, this file is also in Bohr
 u(:) = point_b(:) - point_a(:)
 u(:) = u(:) / NORM2(u)
 allocate(phi(nstate,nspin))

 do ispin=1,nspin
   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'_integral_density.dat'
   open(newunit=line_rho(ispin),file=file_name)
   write(line_rho(ispin),'(a,i3)') '# density integral file generated from MOLGW for spin ',ispin
   write(line_rho(ispin),'(a,f9.5)') '# time_cur = ', time_cur
 enddo

 deltar=NORM2( point_b(:) - point_a(:) )/nr
 do ih=0,nh
   do ispin=1,nspin

     a_cur(:)=point_a(:)+(point_c(:)-point_a(:))*ih/nh
     b_cur(:)=point_b(:)+(point_c(:)-point_a(:))*ih/nh

     integral=0.d0
!     write(stdout,*) ih
     do ir=0,nr
       rr(:) = a_cur(:) + ( b_cur(:) - a_cur(:) ) * ir / nr
       call calculate_basis_functions_r(basis,rr,basis_function_r)
       phi(:,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,:,ispin) )
       integral=integral+SUM( (phi(:,ispin))**2 * occupation(:,ispin) )*deltar
     enddo

     write(line_rho(ispin),'(50(e16.8,2x))') NORM2(a_cur(:)-point_a(:)),integral
   enddo
 enddo

 do ispin=1,nspin
   close(line_rho(ispin))
 end do

 deallocate(phi)

 call stop_clock(timing_print_line_rho_tddft)

end subroutine plot_rho_traj_bunch

!=========================================================================
!subroutine plot_rho_traj_bunch_parallel(nstate,nocc_dim,basis,occupation,c_matrix,num,time_cur)
! implicit none
! integer,intent(in)         :: nstate
! integer,intent(in)         :: nocc_dim
! type(basis_set),intent(in) :: basis
! real(dp),intent(in)        :: occupation(nstate,nspin)
! real(dp),intent(in)        :: c_matrix(basis%nbf,nocc_dim,nspin)
! integer                    :: num
! real(dp),intent(in)        :: time_cur
!!=====
! integer                    :: nr=1000,nh=100
! integer                    :: gt
! integer                    :: nocc(2),nocc_max
! real(dp),parameter         :: length=6.0_dp
! integer                    :: ibf
! integer                    :: istate1,istate2,istate,ispin
! real(dp)                   :: rr(3)
! complex(dp),allocatable    :: phi(:,:)
! real(dp)                   :: point_a(3),point_b(3),point_c(3),u(3)
! real(dp)                   :: a_cur(3), b_cur(3)
! logical                    :: file_exists
! real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
! real(dp)                   :: xxmin,xxmax
! real(dp)                   :: dx,dy,dz
! real(dp)                   :: basis_function_r(basis%nbf)
! integer                    :: ir,ih,iatom
! integer                    :: ibf_cart,ni_cart,ni,li,i_cart
! real(dp),allocatable       :: basis_function_r_cart(:)
! integer,allocatable        :: ocubefile(:,:)
! integer                    :: line_rho(nspin)
! character(len=200)         :: file_name
! integer                    :: linefile
! integer                    :: i_max_atom
! real(dp)                   :: vec_length
! real(dp)                   :: integral, deltar
!!=====
!
!
! call start_clock(timing_print_line_rho_tddft)
!
! gt = get_gaussian_type_tag(basis%gaussian_type)
!
! if( .NOT. in_tddft_loop ) then
!   write(stdout,'(/,1x,a)') 'Plotting electronic density along the projectile trajectory for several impact parameters'
! end if
! ! Find highest occupied state
! nocc = 0
! nocc_max = 0
! do ispin=1,nspin
!   do istate=1,nstate
!     if( occupation(istate,ispin) < completely_empty)  cycle
!     nocc(ispin) = istate
!     if( istate > nocc_max ) nocc_max = istate
!   enddo
!   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
!     call die('Not all occupied states selected in the plot_rho_traj_bunch subroutine')
!   endif
! enddo
!
! inquire(file='manual_dens_traj',exist=file_exists)
! if(file_exists) then
!   open(newunit=linefile,file='manual_dens_traj_tddft',status='old')
!   read(linefile,*) point_a(:)
!   read(linefile,*) point_b(:)
!   read(linefile,*) point_c(:)
!   read(linefile,*) nr
!   read(linefile,*) nh
!
!   close(linefile)
! else
!   point_a = (/ 0.0_dp,  0.0_dp, -10.0_dp  /)
!   point_b = (/ 0.0_dp,  0.0_dp,  10.0_dp  /)
!   point_c = (/ 3.49_dp, 0.0_dp, -10.0_dp  /)
!   call issue_warning('plot_rho_traj_bunch: manual_dens_traj_tddft file was not found')
! endif
!! point_b(:) = point_b(:) / bohr_A
!! point_a(:) = point_a(:) / bohr_A
!! In analogy with cube file, this file is also in Bohr
! u(:) = point_b(:) - point_a(:)
! u(:) = u(:) / NORM2(u)
! allocate(phi(nstate,nspin))
!
!
! deltar=NORM2( point_b(:) - point_a(:) )/nr
!
! integral=0.d0
! do ih=0,nh
!! if(modulo(ih,npoc_world)/=rank_world) cycle
!!integral(ih)
!   do ispin=1,nspin
!
!     a_cur(:)=point_a(:)+(point_c(:)-point_a(:))*ih/nh
!     b_cur(:)=point_b(:)+(point_c(:)-point_a(:))*ih/nh
!
!!     write(stdout,*) ih
!     do ir=0,nr
!       rr(:) = a_cur(:) + ( b_cur(:) - a_cur(:) ) * ir / nr
!       call calculate_basis_functions_r(basis,rr,basis_function_r)
!       phi(:,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,:,ispin) )
!       integral=integral+SUM( (phi(:,ispin))**2 * occupation(:,ispin) )*deltar
!     enddo
!
!   enddo
! enddo
!
! CALL xsum_world(integral(:))
! if( .NOT. is_iomaster ) return
! do ispin=1,nspin
!   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'_integral_density.dat'
!   open(newunit=line_rho(ispin),file=file_name)
!   write(line_rho(ispin),'(a,i3)') '# density integral file generated from MOLGW for spin ',ispin
!   write(line_rho(ispin),'(a,f9.5)') '# time_cur = ', time_cur
! enddo
!!     write(line_rho(ispin),'(50(e16.8,2x))') NORM2(a_cur(:)-point_a(:)),integral
!! norm_array(ih)
!
! do ispin=1,nspin
!   close(line_rho(ispin))
! end do
!
! deallocate(phi)
!
! call stop_clock(timing_print_line_rho_tddft)
!
!end subroutine plot_rho_traj_bunch

!=========================================================================
subroutine plot_rho_traj_bunch_contrib(nstate,basis,occupation,c_matrix,num,time_cur)
 implicit none

 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
 integer                    :: num
 real(dp),intent(in)        :: time_cur
!=====
 integer                    :: nr=1000,nh=100
 integer                    :: gt
 real(dp),parameter         :: length=6.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 real(dp),allocatable       :: phi(:,:)
 real(dp)                   :: point_a(3),point_b(3),point_c(3),u(3)
 real(dp)                   :: a_cur(3), b_cur(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ir,ih,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: line_rho(nspin)
 integer                    :: line_phi_square(nspin)
 character(len=200)         :: file_name
 integer                    :: linefile
 integer                    :: statesfile
 integer                    :: i_max_atom
 integer                    :: nocc
 integer,allocatable        :: istate_cut(:,:)
 real(dp)                   :: vec_length
 real(dp)                   :: deltar,path_length
 real(dp),allocatable       :: integral(:)
 real(dp),allocatable       :: integral_phi_square(:)
 integer                    :: iline,ncut,num_fields,icut
 character(len=500)         :: cur_string
!=====

 if( .NOT. is_iomaster ) return

 nocc=0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty ) cycle
     if( istate > nocc ) nocc = istate
   enddo
 end do

 call start_clock(timing_print_line_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting electronic density along the projectile trajectory for several impact parameters'
 end if

 inquire(file='manual_dens_traj',exist=file_exists)
 if(file_exists) then
   open(newunit=linefile,file='manual_dens_traj',status='old')
   read(linefile,*) point_a(:)
   read(linefile,*) point_b(:)
   read(linefile,*) point_c(:)
   read(linefile,*) nr
   read(linefile,*) nh

   close(linefile)
 else
   point_a = (/ 0.0_dp,  0.0_dp, -10.0_dp  /)
   point_b = (/ 0.0_dp,  0.0_dp,  10.0_dp  /)
   point_c = (/ 3.49_dp, 0.0_dp, -10.0_dp  /)
   call issue_warning('plot_rho_traj_bunch: manual_dens_traj file was not found')
 endif

 inquire(file='manual_dens_traj_states',exist=file_exists)
 if(file_exists) then
   ncut=get_number_of_lines('manual_dens_traj_states')
   allocate(istate_cut(ncut,2))
   open(newunit=statesfile,file='manual_dens_traj_states',status='old')
   do iline=1,ncut
     read(statesfile,'(A)') cur_string
     num_fields = get_number_of_elements(cur_string)
     if( num_fields == 2 ) then
       read(cur_string,*) istate_cut(iline,1), istate_cut(iline,2)
     else if( num_fields == 1) then
       read(cur_string,*) istate_cut(iline,1)
       istate_cut(iline,2) = nstate
     else
       call die("manual_q_matrix_param must contain 1 or two fields.")
     end if

   end do
   close(statesfile)
 else
   ncut=2
   allocate(istate_cut(2,2))
   istate_cut(1,1) = 1; istate_cut(1,2) = 1;
   istate_cut(2,1) = 2; istate_cut(2,2) = nstate;
   call issue_warning('plot_rho_traj_bunch_contrib: manual_dens_traj_states file was not found')
 endif

 allocate(integral(ncut))
 allocate(integral_phi_square(ncut))
! point_b(:) = point_b(:) / bohr_A
! point_a(:) = point_a(:) / bohr_A
! In analogy with cube file, this file is also in Bohr
 u(:) = point_b(:) - point_a(:)
 u(:) = u(:) / NORM2(u)
 allocate(phi(nstate,nspin))

 do ispin=1,nspin
   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'_contribution_integral_density.dat'

   open(newunit=line_rho(ispin),file=file_name)
   write(line_rho(ispin),'(a,i3)') '# density integral file generated from MOLGW for spin ',ispin
   write(line_rho(ispin),'(a,f9.5)') '# time_cur = ', time_cur

   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'_contribution_phi_square.dat'
   open(newunit=line_phi_square(ispin),file=file_name)
   write(line_phi_square(ispin),'(a,i3)') '# density integral file generated from MOLGW for spin ',ispin
   write(line_phi_square(ispin),'(a,f9.5)') '# time_cur = ', time_cur
 enddo

 deltar=NORM2( point_b(:) - point_a(:) )/nr
 path_length=NORM2(point_b(:)-point_a(:))
 do ispin=1,nspin

   do ih=0,nh

     a_cur(:)=point_a(:)+(point_c(:)-point_a(:))*REAL(ih,dp)/REAL(nh,dp)
     b_cur(:)=point_b(:)+(point_c(:)-point_a(:))*REAL(ih,dp)/REAL(nh,dp)

     integral=0.0_dp
     integral_phi_square=0.0_dp

     do ir=0,nr
       rr(:) = a_cur(:) + ( b_cur(:) - a_cur(:) ) * REAL(ir,dp) / REAL(nr,dp)
       call calculate_basis_functions_r(basis,rr,basis_function_r)
       phi(:,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,:,ispin) )

       do icut=1,ncut
         do istate=istate_cut(icut,1),istate_cut(icut,2)
           integral(icut)=integral(icut)+(phi(istate,ispin))**2 * occupation(istate,ispin)
           integral_phi_square(icut)=integral_phi_square(icut)+(phi(istate,ispin))**2
         end do
       end do

     end do
     integral(:)=integral(:)/REAL(nr+1,dp)
     integral_phi_square(:)=integral_phi_square(:)/REAL(nr+1,dp)

     write(line_phi_square(ispin),'(50(f14.8,2x))') NORM2(a_cur(:)-point_a(:)),integral_phi_square(:)
     write(line_rho(ispin),'(50(f14.8,2x))') NORM2(a_cur(:)-point_a(:)),integral(:)
   enddo

 enddo

 do ispin=1,nspin
   close(line_rho(ispin))
   close(line_phi_square(ispin))
 end do

 deallocate(phi)

 call stop_clock(timing_print_line_rho_tddft)

end subroutine plot_rho_traj_bunch_contrib

!=========================================================================
subroutine plot_rho_traj_points_set_contrib(nstate,basis,occupation,c_matrix,num,time_cur)
 implicit none

 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
 integer                    :: num
 real(dp),intent(in)        :: time_cur
!=====
 integer                    :: nr=1000,nh=100
 integer                    :: gt
 real(dp),parameter         :: length=6.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 real(dp),allocatable       :: phi(:,:)
 real(dp)                   :: point_a(3),point_b(3),point_c(3)
 real(dp)                   :: a_cur(3), b_cur(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ir,ih,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: line_rho(nspin)
 integer                    :: line_phi_square(nspin)
 character(len=200)         :: file_name
 integer                    :: points_file
 integer                    :: statesfile
 integer                    :: i_max_atom
 integer                    :: nocc
 integer,allocatable        :: istate_cut(:,:)
 real(dp)                   :: vec_length
 real(dp)                   :: deltar,path_length
 real(dp),allocatable       :: integral(:)
 real(dp),allocatable       :: integral_phi_square(:)
 integer                    :: iline,ncut,num_fields,icut
 character(len=500)         :: cur_string
 integer                    :: npoints,ipoint
 real(dp),allocatable       :: rpoints_start(:,:),rpoints_end(:,:)
!=====

 if( .NOT. is_iomaster ) return

 nocc=0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty ) cycle
     if( istate > nocc ) nocc = istate
   enddo
 end do

 call start_clock(timing_print_line_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting electronic density along the projectile trajectory for several impact parameters'
 end if

 inquire(file='manual_dens_points_set',exist=file_exists)
 if(file_exists) then
   npoints = get_number_of_lines('manual_dens_points_set')
   allocate(rpoints_start(npoints,3))
   allocate(rpoints_end(npoints,3))
   open(newunit=points_file,file='manual_dens_points_set',status='old')
   do iline=1,npoints
     read(points_file,*) rpoints_start(iline,:), rpoints_end(iline,:)
     ! manual_dens_points_set file MUST BE IN ANGSTROMS in contrast to similar files
     rpoints_start(iline,:) = rpoints_start(iline,:) / bohr_A
     rpoints_end(iline,:)   = rpoints_end(iline,:) / bohr_A
   end do
   close(points_file)
 else
   allocate(rpoints_start(1,3))
   allocate(rpoints_end(1,3))
   rpoints_start(1,:) = (/ 0.0_dp,  0.0_dp, 0.0_dp  /)
   rpoints_end(1,:)   = (/ 0.0_dp,  0.0_dp, 5.0_dp  /)
   call issue_warning('plot_rho_traj_bunch: manual_dens_points_set file was not found')
 endif

 inquire(file='manual_dens_traj_states',exist=file_exists)
 if(file_exists) then
   ncut = get_number_of_lines('manual_dens_traj_states')
   allocate(istate_cut(ncut,2))
   open(newunit=statesfile,file='manual_dens_traj_states',status='old')
   do iline=1,ncut
     read(statesfile,'(A)') cur_string
     num_fields = get_number_of_elements(cur_string)
     if( num_fields == 2 ) then
       read(cur_string,*) istate_cut(iline,1), istate_cut(iline,2)
     else if( num_fields == 1) then
       read(cur_string,*) istate_cut(iline,1)
       istate_cut(iline,2) = nstate
     else
       call die("manual_q_matrix_param must contain 1 or two fields.")
     end if

   end do
   close(statesfile)
 else
   ncut=2
   allocate(istate_cut(2,2))
   istate_cut(1,1) = 1; istate_cut(1,2) = nstate;
   call issue_warning('plot_rho_traj_bunch_contrib: manual_dens_traj_states file was not found')
 endif

 allocate(integral(ncut))

 allocate(integral_phi_square(ncut))
 allocate(phi(nstate,nspin))

 do ispin=1,nspin
   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'_points_set_contrib_integral_dens.dat'

   open(newunit=line_rho(ispin),file=file_name)
   write(line_rho(ispin),'(a,i3)') '# density integral file generated from MOLGW for spin ',ispin
   write(line_rho(ispin),'(a,f9.5)') '# time_cur = ', time_cur

   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'_points_set_contrib_phi_square.dat'
   open(newunit=line_phi_square(ispin),file=file_name)
   write(line_phi_square(ispin),'(a,i3)') '# density integral file generated from MOLGW for spin ',ispin
   write(line_phi_square(ispin),'(a,f9.5)') '# time_cur = ', time_cur
 enddo

 do ispin=1,nspin

   do ipoint=1,npoints

     deltar=NORM2( rpoints_end(ipoint,:) - rpoints_start(ipoint,:) )/nr

     integral=0.0_dp
     integral_phi_square=0.0_dp

     do ir=0,nr
       rr(:) = rpoints_start(ipoint,:) + (rpoints_end(ipoint,:) - rpoints_start(ipoint,:)) * REAL(ir,dp) / REAL(nr,dp)
       call calculate_basis_functions_r(basis,rr,basis_function_r)
       phi(:,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,:,ispin) )

       do icut=1,ncut
         do istate=istate_cut(icut,1),istate_cut(icut,2)
           integral(icut)=integral(icut)+(phi(istate,ispin))**2 * occupation(istate,ispin)
           integral_phi_square(icut)=integral_phi_square(icut)+(phi(istate,ispin))**2
         end do
       end do

     end do
     integral(:)=integral(:)/REAL(nr+1,dp)
     integral_phi_square(:)=integral_phi_square(:)/REAL(nr+1,dp)

     ! fixme output ONLY x and y coordinate of rpoints_start
     ! output in ANGSTROMS
     write(line_phi_square(ispin),'(50(f14.8,2x))') rpoints_start(ipoint,1:2)*bohr_A,integral_phi_square(:)
     write(line_rho(ispin),'(50(f14.8,2x))') rpoints_start(ipoint,1:2)*bohr_A,integral(:)
   enddo

 enddo

 do ispin=1,nspin
   close(line_rho(ispin))
   close(line_phi_square(ispin))
 end do

 deallocate(phi)

 call stop_clock(timing_print_line_rho_tddft)

end subroutine plot_rho_traj_points_set_contrib

!=========================================================================
subroutine plot_cube_wfn_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num)

 implicit none
 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 integer                    :: num
!=====
 integer                    :: gt
 integer                    :: nx
 integer                    :: ny
 integer                    :: nz
 integer                    :: nocc(2),nocc_max
 real(dp),parameter         :: length=4.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ix,iy,iz,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: ocuberho(nspin)
 character(len=200)         :: file_name
 integer                    :: icubefile
 integer                    :: i_max_atom
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_cube_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting some selected wavefunctions in a cube file'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_cube_wfn_cmplx')
   endif
 enddo

 istate1= 1
 istate2= nocc_max
 inquire(file='manual_cubewfn_tddft',exist=file_exists)
 if(file_exists) then
   open(newunit=icubefile,file='manual_cubewfn_tddft',status='old')
   read(icubefile,*) nx,ny,nz
   close(icubefile)
 else
   nx=40
   ny=40
   nz=40
 endif
 allocate(phi_cmplx(istate1:istate2,nspin))
 if( .NOT. in_tddft_loop ) then
   write(stdout,'(a,2(2x,i4))')   ' states:   ',istate1,istate2
 end if

 if( excit_type%form==EXCIT_PROJECTILE ) then
   i_max_atom=natom-nprojectile
 else
   i_max_atom=natom
 endif

 xmin =MIN(MINVAL( xatom(1,1:i_max_atom) ),MINVAL( xbasis(1,:) )) - length
 xmax =MAX(MAXVAL( xatom(1,1:i_max_atom) ),MAXVAL( xbasis(1,:) )) + length
 ymin =MIN(MINVAL( xatom(2,1:i_max_atom) ),MINVAL( xbasis(2,:) )) - length
 ymax =MAX(MAXVAL( xatom(2,1:i_max_atom) ),MAXVAL( xbasis(2,:) )) + length
 zmin =MIN(MINVAL( xatom(3,1:i_max_atom) ),MINVAL( xbasis(3,:) )) - length
 zmax =MAX(MAXVAL( xatom(3,1:i_max_atom) ),MAXVAL( xbasis(3,:) )) + length
 dx = (xmax-xmin)/REAL(nx,dp)
 dy = (ymax-ymin)/REAL(ny,dp)
 dz = (zmax-zmin)/REAL(nz,dp)

 do ispin=1,nspin
   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'.cube'
   open(newunit=ocuberho(ispin),file=file_name)
   write(ocuberho(ispin),'(a)') 'cube file generated from MOLGW'
   write(ocuberho(ispin),'(a,i4)') 'density for spin ',ispin
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') natom,xmin,ymin, zmin
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') nx,dx,0.,0.
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') ny,0.,dy,0.
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') nz,0.,0.,dz
   do iatom=1,natom
     write(ocuberho(ispin),'(i6,4(2x,f12.6))') NINT(zatom(iatom)),0.0,xatom(:,iatom)
   enddo
 enddo

 do ix=1,nx
   rr(1) = ( xmin + (ix-1)*dx )
   do iy=1,ny
     rr(2) = ( ymin + (iy-1)*dy )
     do iz=1,nz
       rr(3) = ( zmin + (iz-1)*dz )

       call calculate_basis_functions_r(basis,rr,basis_function_r)

       do ispin=1,nspin
         istate2=nocc(ispin)
         phi_cmplx(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,istate1:istate2,ispin) )
         write(ocuberho(ispin),'(50(e16.8,2x))') SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(istate1:istate2,ispin) ) * spin_fact
       enddo

     enddo
   enddo
 enddo

 do ispin=1,nspin
   close(ocuberho(ispin))
 end do

 deallocate(phi_cmplx)

 call stop_clock(timing_print_cube_rho_tddft)

end subroutine plot_cube_wfn_cmplx

!=========================================================================
subroutine calc_density_in_disc_cmplx_regular(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num,time_cur)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 integer,intent(in)         :: num
 real(dp),intent(in)        :: time_cur
!=====
 integer                    :: gt
 integer                    :: nx
 integer                    :: ny
 integer                    :: nz
 integer                    :: nocc(2),nocc_max
 real(dp)                   :: length
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ix,iy,iz,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: file_out(nspin)
 character(len=200)         :: file_name
 integer                    :: icubefile
 integer                    :: i_max_atom
 real(dp),allocatable       :: charge_layer(:)
!=====

 call start_clock(timing_calc_dens_disc)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Calculate electronic density in discs'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_cube_wfn_cmplx')
   endif
 enddo

 istate1= 1
 istate2= nocc_max

 inquire(file='manual_disc_density',exist=file_exists)
 if(file_exists) then
   open(newunit=icubefile,file='manual_disc_density',status='old')
   read(icubefile,*) nx,ny,nz
   read(icubefile,*) length
   close(icubefile)
 else
   nx=40
   ny=40
   nz=256
   length=10.0_dp
   call issue_warning('calc_density_in_disc_cmplx_regular: manual file was not found')
 endif
 allocate(phi_cmplx(istate1:istate2,nspin))

 if( excit_type%form==EXCIT_PROJECTILE ) then
   i_max_atom=natom-nprojectile
 else
   i_max_atom=natom
 endif

 xmin =MINVAL( xatom(1,1:i_max_atom) ) - length
 xmax =MAXVAL( xatom(1,1:i_max_atom) ) + length
 ymin =MINVAL( xatom(2,1:i_max_atom) ) - length
 ymax =MAXVAL( xatom(2,1:i_max_atom) ) + length
 zmin =MINVAL( xatom(3,1:i_max_atom) ) - length
 zmax =MAXVAL( xatom(3,1:i_max_atom) ) + length

 dx = (xmax-xmin)/REAL(nx,dp)
 dy = (ymax-ymin)/REAL(ny,dp)
 dz = (zmax-zmin)/REAL(nz,dp)

 if( is_iomaster ) then
   do ispin=1,nspin
     write(file_name,'(a,i4.4,a,i1,a,i3.3,f0.3,a)') 'disc_dens_',num, "_s_",ispin,"_r_",INT(r_disc),r_disc-INT(r_disc),".dat"
     open(newunit=file_out(ispin),file=file_name)
     write(file_out(ispin),'(a,F12.6,a,3F12.6)') '# Time: ',time_cur, '  Projectile position (A): ',xatom(:,natom)*bohr_A
   enddo
 end if

 allocate(charge_layer(nz))

 do ispin=1,nspin
   istate2=nocc(ispin)
   charge_layer(:)=0.0_dp
   do iz=1,nz
     if(MODULO(iz-1,nproc_world)/=rank_world) cycle
     rr(3) = ( zmin + (iz-1)*dz )
     do ix=1,nx
       rr(1) = ( xmin + (ix-1)*dx )
       do iy=1,ny
         rr(2) = ( ymin + (iy-1)*dy )

         if( (rr(1)**2+rr(2)**2)**0.5_dp <= r_disc ) then
           call calculate_basis_functions_r(basis,rr,basis_function_r)
           phi_cmplx(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,istate1:istate2,ispin) )
           charge_layer(iz)=charge_layer(iz)+SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(istate1:istate2,ispin) ) * spin_fact
         end if

       enddo
     enddo
   enddo

   call xsum_world(charge_layer(:))

   charge_layer = charge_layer * dx*dy

   if( is_iomaster ) then
     do iz=1,nz
       rr(3) = ( zmin + (iz-1)*dz )
       write(file_out(ispin),'(F16.4,F19.10)') rr(3)*bohr_A,charge_layer(iz)
     end do
     close(file_out(ispin))
   end if

 enddo ! ispin

 deallocate(phi_cmplx)

 call stop_clock(timing_calc_dens_disc)

end subroutine calc_density_in_disc_cmplx_regular

!=========================================================================
subroutine calc_cube_initial_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,cube_density_start,nx,ny,nz)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 real(dp),intent(inout)     :: cube_density_start(nx,ny,nz,nspin)
 integer,intent(in)         :: nx
 integer,intent(in)         :: ny
 integer,intent(in)         :: nz
!=====
 integer                    :: gt
 integer                    :: nocc(2),nocc_max
 real(dp),parameter         :: length=4.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ix,iy,iz,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: icubefile
 integer                    :: i_max_atom
 integer                    :: ndim1,ndim2,ndim3
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_cube_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting some selected wavefunctions in a cube file'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_cube_wfn_cmplx')
   endif
 enddo

 istate1= 1
 istate2= nocc_max

 allocate(phi_cmplx(istate1:istate2,nspin))
 if( .NOT. in_tddft_loop ) then
   write(stdout,'(a,2(2x,i4))')   ' states:   ',istate1,istate2
 end if

 if( excit_type%form==EXCIT_PROJECTILE ) then
   i_max_atom=natom-nprojectile
 else
   i_max_atom=natom
 endif

 xmin =MIN(MINVAL( xatom(1,1:i_max_atom) ),MINVAL( xbasis(1,:) )) - length
 xmax =MAX(MAXVAL( xatom(1,1:i_max_atom) ),MAXVAL( xbasis(1,:) )) + length
 ymin =MIN(MINVAL( xatom(2,1:i_max_atom) ),MINVAL( xbasis(2,:) )) - length
 ymax =MAX(MAXVAL( xatom(2,1:i_max_atom) ),MAXVAL( xbasis(2,:) )) + length
 zmin =MIN(MINVAL( xatom(3,1:i_max_atom) ),MINVAL( xbasis(3,:) )) - length
 zmax =MAX(MAXVAL( xatom(3,1:i_max_atom) ),MAXVAL( xbasis(3,:) )) + length
 dx = (xmax-xmin)/REAL(nx,dp)
 dy = (ymax-ymin)/REAL(ny,dp)
 dz = (zmax-zmin)/REAL(nz,dp)

 do ix=1,nx
   rr(1) = ( xmin + (ix-1)*dx )
   do iy=1,ny
     rr(2) = ( ymin + (iy-1)*dy )
     do iz=1,nz
       rr(3) = ( zmin + (iz-1)*dz )

       call calculate_basis_functions_r(basis,rr,basis_function_r)

       do ispin=1,nspin
         istate2=nocc(ispin)
         phi_cmplx(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,istate1:istate2,ispin) )
         cube_density_start(ix,iy,iz,ispin)=SUM( abs(phi_cmplx(:,ispin))**2 * occupation(istate1:istate2,ispin) ) * spin_fact
       enddo

     enddo
   enddo
 enddo

 deallocate(phi_cmplx)

 call stop_clock(timing_print_cube_rho_tddft)

end subroutine calc_cube_initial_cmplx

!=========================================================================
subroutine initialize_cube_diff_cmplx(nx,ny,nz)
 implicit none
 integer,intent(inout)      :: nx,ny,nz
!=====
 integer                    :: unit_cube_diff
 logical                    :: file_exists

 inquire(file='manual_cube_diff_tddft',exist=file_exists)
 if(file_exists) then
   open(newunit=unit_cube_diff,file='manual_cube_diff_tddft',status='old')
   read(unit_cube_diff,*) nx,ny,nz
   close(unit_cube_diff)
 else
   nx=40
   ny=40
   nz=40
 endif

end subroutine initialize_cube_diff_cmplx

!=========================================================================
subroutine plot_cube_diff_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num,cube_density_start,nx,ny,nz)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 real(dp),intent(in)        :: cube_density_start(nx,ny,nz,nspin)
 integer,intent(in)         :: num
 integer,intent(in)         :: nx
 integer,intent(in)         :: ny
 integer,intent(in)         :: nz
!=====
 integer                    :: gt
 integer                    :: nocc(2),nocc_max
 real(dp),parameter         :: length=4.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ix,iy,iz,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: ocuberho(nspin)
 character(len=200)         :: file_name
 integer                    :: icubefile
 integer                    :: i_max_atom
 integer                    :: ndim1,ndim2,ndim3
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_cube_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting some selected wavefunctions in a cube file'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_cube_wfn_cmplx')
   endif
 enddo

 istate1= 1
 istate2= nocc_max

 allocate(phi_cmplx(istate1:istate2,nspin))
 if( .NOT. in_tddft_loop ) then
   write(stdout,'(a,2(2x,i4))')   ' states:   ',istate1,istate2
 end if

 if( excit_type%form==EXCIT_PROJECTILE ) then
   i_max_atom=natom-nprojectile
 else
   i_max_atom=natom
 endif

 xmin =MIN(MINVAL( xatom(1,1:i_max_atom) ),MINVAL( xbasis(1,:) )) - length
 xmax =MAX(MAXVAL( xatom(1,1:i_max_atom) ),MAXVAL( xbasis(1,:) )) + length
 ymin =MIN(MINVAL( xatom(2,1:i_max_atom) ),MINVAL( xbasis(2,:) )) - length
 ymax =MAX(MAXVAL( xatom(2,1:i_max_atom) ),MAXVAL( xbasis(2,:) )) + length
 zmin =MIN(MINVAL( xatom(3,1:i_max_atom) ),MINVAL( xbasis(3,:) )) - length
 zmax =MAX(MAXVAL( xatom(3,1:i_max_atom) ),MAXVAL( xbasis(3,:) )) + length
 dx = (xmax-xmin)/REAL(nx,dp)
 dy = (ymax-ymin)/REAL(ny,dp)
 dz = (zmax-zmin)/REAL(nz,dp)

 do ispin=1,nspin
   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'dens_diff.cube'
   open(newunit=ocuberho(ispin),file=file_name)
   write(ocuberho(ispin),'(a)') 'cube file generated from MOLGW'
   write(ocuberho(ispin),'(a,i4)') 'density difference for spin ',ispin
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') natom,xmin,ymin, zmin
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') nx,dx,0.,0.
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') ny,0.,dy,0.
   write(ocuberho(ispin),'(i6,3(f12.6,2x))') nz,0.,0.,dz
   do iatom=1,natom
     write(ocuberho(ispin),'(i6,4(2x,f12.6))') NINT(zatom(iatom)),0.0,xatom(:,iatom)
   enddo
 enddo

 do ix=1,nx
   rr(1) = ( xmin + (ix-1)*dx )
   do iy=1,ny
     rr(2) = ( ymin + (iy-1)*dy )
     do iz=1,nz
       rr(3) = ( zmin + (iz-1)*dz )

!       call start_clock(timing_tmp0)
       call calculate_basis_functions_r(basis,rr,basis_function_r)
!       call stop_clock(timing_tmp0)

       do ispin=1,nspin
         istate2=nocc(ispin)
         phi_cmplx(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,istate1:istate2,ispin) )
!       call start_clock(timing_tmp1)
         write(ocuberho(ispin),'(50(e16.8,2x))') SUM( abs(phi_cmplx(:,ispin))**2 * occupation(istate1:istate2,ispin) ) * spin_fact - cube_density_start(ix,iy,iz,ispin)
!       call stop_clock(timing_tmp1)
       enddo

     enddo
   enddo
 enddo

 do ispin=1,nspin
   close(ocuberho(ispin))
 end do

 deallocate(phi_cmplx)

 call stop_clock(timing_print_cube_rho_tddft)

end subroutine plot_cube_diff_cmplx

!=========================================================================
subroutine plot_cube_diff_parallel_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num,cube_density_start,nx,ny,nz)

 implicit none
 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 real(dp),intent(in)        :: cube_density_start(nx,ny,nz,nspin)
 integer,intent(in)         :: num
 integer,intent(in)         :: nx
 integer,intent(in)         :: ny
 integer,intent(in)         :: nz
!=====
 integer                    :: gt
 integer                    :: nocc(2),nocc_max
 real(dp),parameter         :: length=4.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: u(3),a(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ix,iy,iz,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: ocuberho(nspin)
 character(len=200)         :: file_name
 integer                    :: icubefile
 integer                    :: i_max_atom
 integer                    :: ndim1,ndim2,ndim3
 real(dp),allocatable       :: dens_diff(:,:,:)
!=====

 call start_clock(timing_print_cube_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting some selected wavefunctions in a cube file'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_cube_wfn_cmplx')
   endif
 enddo

 istate1= 1
 istate2= nocc_max

 allocate(phi_cmplx(istate1:istate2,nspin))
 if( .NOT. in_tddft_loop ) then
   write(stdout,'(a,2(2x,i4))')   ' states:   ',istate1,istate2
 end if

 if( excit_type%form==EXCIT_PROJECTILE ) then
   i_max_atom=natom-nprojectile
 else
   i_max_atom=natom
 endif

 xmin =MIN(MINVAL( xatom(1,1:i_max_atom) ),MINVAL( xbasis(1,:) )) - length
 xmax =MAX(MAXVAL( xatom(1,1:i_max_atom) ),MAXVAL( xbasis(1,:) )) + length
 ymin =MIN(MINVAL( xatom(2,1:i_max_atom) ),MINVAL( xbasis(2,:) )) - length
 ymax =MAX(MAXVAL( xatom(2,1:i_max_atom) ),MAXVAL( xbasis(2,:) )) + length
 zmin =MIN(MINVAL( xatom(3,1:i_max_atom) ),MINVAL( xbasis(3,:) )) - length
 zmax =MAX(MAXVAL( xatom(3,1:i_max_atom) ),MAXVAL( xbasis(3,:) )) + length
 dx = (xmax-xmin)/REAL(nx,dp)
 dy = (ymax-ymin)/REAL(ny,dp)
 dz = (zmax-zmin)/REAL(nz,dp)

 if( is_iomaster ) then
   do ispin=1,nspin
     write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'dens_diff.cube'
     open(newunit=ocuberho(ispin),file=file_name)
     write(ocuberho(ispin),'(a)') 'cube file generated from MOLGW'
     write(ocuberho(ispin),'(a,i4)') 'density difference for spin ',ispin
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') natom,xmin,ymin, zmin
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') nx,dx,0.,0.
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') ny,0.,dy,0.
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') nz,0.,0.,dz
     do iatom=1,natom
       write(ocuberho(ispin),'(i6,4(2x,f12.6))') NINT(zatom(iatom)),0.0,xatom(:,iatom)
     enddo
   enddo
 end if

 call clean_allocate("dens_diff for the cube density",dens_diff,nx,ny,nz)

 do ispin=1,nspin
   istate2=nocc(ispin)
   dens_diff=0.d0
   call start_clock(timing_tmp0)
   !$OMP PARALLEL PRIVATE(basis_function_r,rr,ix,iy,iz,phi_cmplx)
   !$OMP DO
   do ix=1,nx
!     if(MODULO(ix-1,nproc_world)/=rank_world) cycle
     rr(1) = ( xmin + (ix-1)*dx )
     do iy=1,ny
       rr(2) = ( ymin + (iy-1)*dy )
       do iz=1,nz
         rr(3) = ( zmin + (iz-1)*dz )

         call calculate_basis_functions_r(basis,rr,basis_function_r)

         phi_cmplx(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,istate1:istate2,ispin) )
         dens_diff(ix,iy,iz) = SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(istate1:istate2,ispin) ) * spin_fact - cube_density_start(ix,iy,iz,ispin)

       enddo

     enddo
   enddo
   !$OMP END DO
   !$OMP END PARALLEL
   call stop_clock(timing_tmp0)

!   call start_clock(timing_tmp1)
!   call xsum_world(dens_diff)
!   call stop_clock(timing_tmp1)

   if( is_iomaster ) then
     call start_clock(timing_tmp2)
     do ix=1,nx
       do iy=1,ny
         do iz=1,nz
           write(ocuberho(ispin),'(50(e16.8,2x))') dens_diff(ix,iy,iz)
         end do
       end do
     end do
     call stop_clock(timing_tmp2)
   end if

 enddo !do ispin

 do ispin=1,nspin
   close(ocuberho(ispin))
 end do

 deallocate(phi_cmplx)

 call stop_clock(timing_print_cube_rho_tddft)

end subroutine plot_cube_diff_parallel_cmplx

!=========================================================================
subroutine plot_rho_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num,time_cur)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 integer,intent(in)         :: num
 real(dp),intent(in)        :: time_cur
!=====
 integer                    :: nr
 integer                    :: gt
 integer                    :: nocc(2),nocc_max
 real(dp),parameter         :: length=6.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: point_a(3),point_b(3),u(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ir,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: line_rho(nspin)
 character(len=200)         :: file_name
 integer                    :: linefile
 integer                    :: i_max_atom
 real(dp)                   :: vec_length
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_line_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting some selected wavefunctions along one line'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_cube_wfn_cmplx')
   endif
 enddo

 inquire(file='manual_plot_rho_tddft',exist=file_exists)
 if(file_exists) then
   open(newunit=linefile,file='manual_plot_rho_tddft',status='old')
   read(linefile,*) nr
   read(linefile,*) point_a(:)
   read(linefile,*) point_b(:)
   close(linefile)
 else
   nr = 5000
   point_a = (/ 0.0_dp, 0.0_dp, 0.0_dp  /)
   point_b = (/ 0.0_dp, 0.0_dp, 10.0_dp  /)
   call issue_warning('plot_line_wfn_cmplx: manual_plot_rho_tddft file was not found')
 endif
! point_b(:) = point_b(:) / bohr_A
! point_a(:) = point_a(:) / bohr_A
! In analogy with cube file, this file is also in Bohr
 u(:) = point_b(:) - point_a(:)
 u(:) = u(:) / NORM2(u)
 allocate(phi_cmplx(nocc_dim,nspin))

 do ispin=1,nspin
   write(file_name,'(i4.4,a,i1,a)') num,'_',ispin,'_line_density.dat'
   open(newunit=line_rho(ispin),file=file_name)
!   write(line_rho(ispin),'(a,i3)') '# line density file generated from MOLGW for spin ',ispin
   write(line_rho(ispin),'(a,F12.6,a,3F12.6)') '# Time: ',time_cur, '  Projectile position (A): ',xatom(:,natom)*bohr_A
 enddo

 do ir=0,nr
   rr(:) = (ir + 0.0_dp) / nr * ( point_b(:) - point_a(:) ) + point_a(:)

   call calculate_basis_functions_r(basis,rr,basis_function_r)

   do ispin=1,nspin
     phi_cmplx(:,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,:,ispin) )
     write(line_rho(ispin),'(50(e16.8,2x))') DOT_PRODUCT(rr(:),u(:)),SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(:nocc_dim,ispin) )
   enddo
 enddo

 do ispin=1,nspin
   close(line_rho(ispin))
 end do

 deallocate(phi_cmplx)

 call stop_clock(timing_print_line_rho_tddft)

end subroutine plot_rho_cmplx

!=========================================================================
subroutine plot_rho_diff_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num,time_cur,nr_line_rho,point_a,point_b,rho_start)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 integer,intent(in)         :: num
 real(dp),intent(in)        :: time_cur
 real(dp),intent(in)        :: point_a(3),point_b(3)
 integer,intent(in)         :: nr_line_rho
 real(dp),intent(out)       :: rho_start(nr_line_rho,nspin)
!=====
 integer                    :: ir,ispin,gt
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: u(3)
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: line_rho(nspin)
 character(len=200)         :: file_name
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_line_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 write(stdout,'(/,1x,a)') 'Plotting the electronic density along one line'

 u(:) = point_b(:) - point_a(:)
 u(:) = u(:) / NORM2(u)
 allocate(phi_cmplx(nocc_dim,nspin))

 do ispin=1,nspin
   write(file_name,'(a,i4.4,a,i1,a)') 'diff_',num,'_',ispin,'_line_density.dat'
   open(newunit=line_rho(ispin),file=file_name)
   write(line_rho(ispin),'(a,F12.6,a,3F12.6)') '# Time: ',time_cur, '  Projectile position (A): ',xatom(:,natom)*bohr_A
 enddo

 do ir=1,nr_line_rho
   rr(:) = (ir - 1.d0) / (nr_line_rho - 1) * ( point_b(:) - point_a(:) ) + point_a(:)

   call calculate_basis_functions_r(basis,rr,basis_function_r)

   do ispin=1,nspin
     phi_cmplx(:,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,:,ispin) )
     write(line_rho(ispin),'(50(e16.8,2x))') DOT_PRODUCT(rr(:),u(:)),SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(:nocc_dim,ispin) ) - rho_start(ir,ispin)
   enddo
 enddo

 do ispin=1,nspin
   close(line_rho(ispin))
 end do

 deallocate(phi_cmplx)

 call stop_clock(timing_print_line_rho_tddft)

end subroutine plot_rho_diff_cmplx

!=========================================================================
subroutine calc_rho_initial_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num,time_cur,nr_line_rho,point_a,point_b,rho_start)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 integer,intent(in)         :: num
 real(dp),intent(in)        :: time_cur
 real(dp),intent(in)        :: point_a(3),point_b(3)
 integer,intent(in)         :: nr_line_rho
 real(dp),intent(out)       :: rho_start(nr_line_rho,nspin)
!=====
 integer                    :: gt,ispin,ir
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: u(3)
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: line_rho(nspin)
 character(len=200)         :: file_name
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_line_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 write(stdout,'(/,1x,a)') 'Calculating initial electronic denstiy along one line'

 u(:) = point_b(:) - point_a(:)
 u(:) = u(:) / NORM2(u)
 allocate(phi_cmplx(nocc_dim,nspin))

 do ispin=1,nspin
   write(file_name,'(a)') 'total_initial_line_density.dat'
   open(newunit=line_rho(ispin),file=file_name)
   write(line_rho(ispin),'(a,F12.6,a,3F12.6)') '# Time: ',time_cur, '  Projectile position (A): ',xatom(:,natom)*bohr_A
 enddo

 do ir=1,nr_line_rho
   rr(:) = (ir - 1.d0) / (nr_line_rho - 1) * ( point_b(:) - point_a(:) ) + point_a(:)

   call calculate_basis_functions_r(basis,rr,basis_function_r)

   do ispin=1,nspin
     phi_cmplx(:,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,:,ispin) )
     rho_start(ir,ispin) = SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(:nocc_dim,ispin) )
     write(line_rho(ispin),'(50(e16.8,2x))') DOT_PRODUCT(rr(:),u(:)),rho_start(ir,ispin)
   enddo
 enddo

 do ispin=1,nspin
   close(line_rho(ispin))
 end do

 deallocate(phi_cmplx)

 call stop_clock(timing_print_line_rho_tddft)

end subroutine calc_rho_initial_cmplx

!=========================================================================
subroutine initialize_rho_diff_cmplx(nr_line_rho,point_a,point_b)
 implicit none

 real(dp),intent(out)       :: point_a(3),point_b(3)
 integer,intent(out)        :: nr_line_rho
!=====
 logical                    :: file_exists
 integer                    :: linefile
!=====

 if( .NOT. is_iomaster ) return

 inquire(file='manual_plot_rho_tddft',exist=file_exists)
 if(file_exists) then
   open(newunit=linefile,file='manual_plot_rho_tddft',status='old')
   read(linefile,*) nr_line_rho
   read(linefile,*) point_a(:)
   read(linefile,*) point_b(:)
   close(linefile)
 else
   nr_line_rho=5000
   point_a = (/ 0.0_dp, 0.0_dp, 0.0_dp  /)
   point_b = (/ 0.0_dp, 0.0_dp, 10.0_dp  /)
   call issue_warning('initialize_rho_diff_cmplx: manual_plot_rho_tddft file was not found')
 endif

end subroutine initialize_rho_diff_cmplx

!=========================================================================
subroutine plot_rho_traj_bunch_cmplx(nstate,nocc_dim,basis,occupation,c_matrix_cmplx,num,time_cur)
 implicit none

 integer,intent(in)         :: nstate
 integer,intent(in)         :: nocc_dim
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 complex(dp),intent(in)     :: c_matrix_cmplx(basis%nbf,nocc_dim,nspin)
 integer                    :: num
 real(dp),intent(in)        :: time_cur
!=====
 integer                    :: nr=1000,nh=100
 integer                    :: gt
 integer                    :: nocc(2),nocc_max
 real(dp),parameter         :: length=6.0_dp
 integer                    :: ibf
 integer                    :: istate1,istate2,istate,ispin
 real(dp)                   :: rr(3)
 complex(dp),allocatable    :: phi_cmplx(:,:)
 real(dp)                   :: point_a(3),point_b(3),point_c(3),u(3)
 real(dp)                   :: a_cur(3), b_cur(3)
 logical                    :: file_exists
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: xxmin,xxmax
 real(dp)                   :: dx,dy,dz
 real(dp)                   :: basis_function_r(basis%nbf)
 integer                    :: ir,ih,iatom
 integer                    :: ibf_cart,ni_cart,ni,li,i_cart
 real(dp),allocatable       :: basis_function_r_cart(:)
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: line_rho(nspin)
 character(len=200)         :: file_name
 integer                    :: linefile
 integer                    :: i_max_atom
 real(dp)                   :: vec_length
 real(dp)                   :: integral, deltar
!=====

 if( .NOT. is_iomaster ) return

 call start_clock(timing_print_line_rho_tddft)

 gt = get_gaussian_type_tag(basis%gaussian_type)

 if( .NOT. in_tddft_loop ) then
   write(stdout,'(/,1x,a)') 'Plotting electronic density along the projectile trajectory for several impact parameters'
 end if
 ! Find highest occupied state
 nocc = 0
 nocc_max = 0
 do ispin=1,nspin
   do istate=1,nstate
     if( occupation(istate,ispin) < completely_empty)  cycle
     nocc(ispin) = istate
     if( istate > nocc_max ) nocc_max = istate
   enddo
   if( .NOT. (ALL( occupation(nocc(ispin)+1,:) < completely_empty )) ) then
     call die('Not all occupied states selected in the plot_cube_wfn_cmplx')
   endif
 enddo

 inquire(file='manual_dens_traj_tddft',exist=file_exists)
 if(file_exists) then
   open(newunit=linefile,file='manual_dens_traj_tddft',status='old')
   read(linefile,*) point_a(:)
   read(linefile,*) point_b(:)
   read(linefile,*) point_c(:)
   read(linefile,*) nr
   read(linefile,*) nh

   close(linefile)
 else
   point_a = (/ 0.0_dp,  0.0_dp, -10.0_dp  /)
   point_b = (/ 0.0_dp,  0.0_dp,  10.0_dp  /)
   point_c = (/ 3.49_dp, 0.0_dp, -10.0_dp  /)
   call issue_warning('plot_line_wfn_cmplx: manual_dens_traj_tddft file was not found')
 endif
! point_b(:) = point_b(:) / bohr_A
! point_a(:) = point_a(:) / bohr_A
! In analogy with cube file, this file is also in Bohr
 u(:) = point_b(:) - point_a(:)
 u(:) = u(:) / NORM2(u)
 allocate(phi_cmplx(nstate,nspin))

 do ispin=1,nspin
   write(file_name,'(i3.3,a,i1,a)') num,'_',ispin,'_integral_density.dat'
   open(newunit=line_rho(ispin),file=file_name)
   write(line_rho(ispin),'(a,i3)') '# density integral file generated from MOLGW for spin ',ispin
   write(line_rho(ispin),'(a,f9.5)') '# time_cur = ', time_cur
 enddo

 deltar=NORM2( point_b(:) - point_a(:) )/nr
 do ih=0,nh
   do ispin=1,nspin

     a_cur(:)=point_a(:)+(point_c(:)-point_a(:))*ih/nh
     b_cur(:)=point_b(:)+(point_c(:)-point_a(:))*ih/nh

     integral=0.d0
!     write(stdout,*) ih
     do ir=0,nr
       rr(:) = a_cur(:) + ( b_cur(:) - a_cur(:) ) * ir / nr
       call calculate_basis_functions_r(basis,rr,basis_function_r)
       phi_cmplx(:,ispin) = MATMUL( basis_function_r(:) , c_matrix_cmplx(:,:,ispin) )
       integral=integral+SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(:,ispin) )*deltar
     enddo

     write(line_rho(ispin),'(50(e16.8,2x))') NORM2(a_cur(:)-point_a(:)),integral,SUM( ABS(phi_cmplx(:,ispin))**2 * occupation(:,ispin) )
   enddo
 enddo

 do ispin=1,nspin
   close(line_rho(ispin))
 end do

 deallocate(phi_cmplx)

 call stop_clock(timing_print_line_rho_tddft)

end subroutine plot_rho_traj_bunch_cmplx


!=========================================================================
subroutine read_cube_wfn(nstate,basis,occupation,c_matrix)
 implicit none
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: occupation(nstate,nspin)
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
!=====
 integer                    :: icubefile
 character(len=200)         :: file_name,tmp
 real(dp),allocatable       :: phi(:,:),pot(:,:,:)
 real(dp),allocatable       :: pot_i(:,:)
 real(dp)                   :: rr(3)
 real(dp)                   :: xmin,xmax,ymin,ymax,zmin,zmax
 real(dp)                   :: dr(3,3)
 real(dp)                   :: basis_function_r(basis%nbf)
 real(dp)                   :: chi2
 integer                    :: natom1
 integer                    :: nx,ny,nz
 integer                    :: ix,iy,iz
 integer                    :: istate1,istate2,istate,ispin,iatom
 integer,allocatable        :: ocubefile(:,:)
 integer                    :: ocuberho(nspin)
 real(dp)                   :: dv
 integer                    :: stride=6
!=====

 if( .NOT. is_iomaster ) return

 file_name = 'in.cube'
 open(newunit=icubefile,file=TRIM(file_name),action='read',status='old')
 read(icubefile,*) tmp
 read(icubefile,*) tmp
 read(icubefile,*) natom1,xmin,ymin,zmin
 read(icubefile,*) nx,dr(:,1)
 read(icubefile,*) ny,dr(:,2)
 read(icubefile,*) nz,dr(:,3)
 do iatom=1,natom1
   read(icubefile,*)
 enddo
 dv = determinant_3x3_matrix(dr)
 allocate(pot(nx,ny,nz))
 do ix=1,nx
   do iy=1,ny
     do iz=1,(nz/stride-1)*stride+1,stride
       read(icubefile,*) pot(ix,iy,iz:iz+stride-1)
     enddo
     if( modulo(nz,stride) /=0 ) read(icubefile,*) pot(ix,iy,(nz/stride)*stride+1:nz)
   enddo
 enddo
 close(icubefile)
 write(stdout,*) 'normalization:',SUM(pot(:,:,:))*dv

 istate1 = 2
 istate2 = 2

 allocate(phi(istate1:istate2,nspin))
 allocate(pot_i(istate1:istate2,nspin))
 allocate(ocubefile(istate1:istate2,nspin))

 !
 ! check whether istate1:istate2 spans all the occupied states
 if( ALL( occupation(istate2+1,:) < completely_empty ) ) then
   do ispin=1,nspin
     write(file_name,'(a,i1,a)') 'rho_',ispin,'.cube'
     open(newunit=ocuberho(ispin),file=file_name)
     write(ocuberho(ispin),'(a)') 'cube file generated from MOLGW'
     write(ocuberho(ispin),'(a,i4)') 'density for spin ',ispin
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') natom,xmin,ymin,zmin
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') nx,dr(:,1)
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') ny,dr(:,2)
     write(ocuberho(ispin),'(i6,3(f12.6,2x))') nz,dr(:,3)
     do iatom=1,natom
       write(ocuberho(ispin),'(i6,4(2x,f12.6))') NINT(zatom(iatom)),0.0,xatom(:,iatom)
     enddo
   enddo
 endif

 do istate=istate1,istate2
   do ispin=1,nspin
     write(file_name,'(a,i3.3,a,i1,a)') 'wfn_',istate,'_',ispin,'.cube'
     open(newunit=ocubefile(istate,ispin),file=file_name)
     write(ocubefile(istate,ispin),'(a)') 'cube file generated from MOLGW'
     write(ocubefile(istate,ispin),'(a,i4)') 'wavefunction ',istate1
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') natom,xmin,ymin,zmin
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') nx,dr(:,1)
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') ny,dr(:,2)
     write(ocubefile(istate,ispin),'(i6,3(f12.6,2x))') nz,dr(:,3)
     do iatom=1,natom
       write(ocubefile(istate,ispin),'(i6,4(2x,f12.6))') NINT(zatom(iatom)),0.0,xatom(:,iatom)
     enddo
   enddo
 enddo

 pot_i(:,:) = 0.0_dp
 chi2 = 0.0_dp
 do ix=1,nx
   do iy=1,ny
     do iz=1,nz
       rr(1) = xmin
       rr(2) = ymin
       rr(3) = zmin
       rr(:) = rr(:) + (ix-1) * dr(:,1) + (iy-1) * dr(:,2) + (iz-1) * dr(:,3)

       call calculate_basis_functions_r(basis,rr,basis_function_r)

       do ispin=1,nspin
         phi(istate1:istate2,ispin) = MATMUL( basis_function_r(:) , c_matrix(:,istate1:istate2,ispin) )
       enddo

       do istate=istate1,istate2
         do ispin=1,nspin
           write(ocubefile(istate,ispin),'(50(e16.8,2x))') phi(istate,ispin)
           pot_i(istate,ispin) = pot_i(istate,ispin) - pot(ix,iy,iz) * phi(istate,ispin)**2 * dv
         enddo
       enddo

       !
       ! check whether istate1:istate2 spans all the occupied states
       if( ALL( occupation(istate2+1,:) < completely_empty ) ) then
         do ispin=1,nspin
           write(ocuberho(ispin),'(50(e16.8,2x))') SUM( phi(:,ispin)**2 * occupation(istate1:istate2,ispin) )
           chi2 = chi2 + ( pot(ix,iy,iz) - SUM( phi(:,ispin)**2 * occupation(istate1:istate2,ispin) ) )**2
         enddo
       endif


     enddo
   enddo
 enddo

 deallocate(phi,pot,pot_i)
 deallocate(ocubefile)

end subroutine read_cube_wfn


!=========================================================================
subroutine read_gaussian_fchk(read_fchk_in,file_name,basis,p_matrix_out)
 implicit none

 character(len=*),intent(in) :: read_fchk_in
 character(len=*),intent(in) :: file_name
 type(basis_set),intent(in)  :: basis
 real(dp),intent(out)        :: p_matrix_out(basis%nbf,basis%nbf,nspin)
!=====
 integer,parameter :: stride=5
 logical :: file_exists,found
 integer :: fu
 integer :: istat
 integer :: ispin,ibf,jbf,ijbf
 integer :: nel
 integer :: block_d(6,6)
 integer :: block_f(10,10)
 integer :: block_g(15,15)
 integer :: block_h(21,21)
 integer :: block_j(28,28)
 real(dp) :: swap(basis%nbf,basis%nbf)
 real(dp),allocatable :: p_matrix_read(:)
 character(len=256) :: line
 character(len=100) :: keyword
!=====

 p_matrix_out(:,:,:) = 0.0_dp


 if( nspin /= 1 ) then
   call issue_warning('read_gaussian_fchk: unrestricted calculations not coded yet')
   return
 endif

 if( basis%gaussian_type /= 'CART' ) then
   call issue_warning('read_gaussian_fchk: Pure gaussians not coded yet')
   return
 endif

 if( is_iomaster ) then

   select case(TRIM(read_fchk_in))
   case('CC')
     keyword = 'Total CC Density'
   case('MP2')
     keyword = 'Total MP2 Density'
   case('SCF')
     keyword = 'Total SCF Density'
   case default
     write(stdout,*) read_fchk_in
     call die('read_gaussian_fchk: invalid choice for input variable read_fchk')
   end select

   write(stdout,'(/,1x,a,a)') 'Reading an existing Gaussian formatted checkpoint point: ',&
                              TRIM(file_name)
   write(stdout,'(1x,a,a)')   'Reading field: ',TRIM(keyword)

   inquire(file=file_name,exist=file_exists)
   if( .NOT. file_exists) then
     call issue_warning('File not found: ' // TRIM(file_name))
     return
   endif

   write(stdout,'(1x,a,a)') 'Density matrix read: ',TRIM(read_fchk_in)

   nel = (basis%nbf*(basis%nbf+1))/2
   allocate(p_matrix_read(nel))


   open(newunit=fu,file=TRIM(file_name),status='old',action='read')

   ! Read the fchk file until the keyword is found
   found = .FALSE.
   do while( .NOT. found )
     read(fu,'(a)',iostat=istat) line
     if( IS_IOSTAT_END(istat) ) then
       call issue_warning(TRIM(keyword)//' not found in file')
       return
     endif
     found = ( INDEX(line,TRIM(keyword)) /= 0 )
   enddo

   do ispin=1,nspin
     do ijbf=1,(nel/stride-1)*stride+1,stride
       read(fu,*) p_matrix_read(ijbf:ijbf+stride-1)
     enddo
     if( MODULO(nel,stride) /=0 ) read(fu,*) p_matrix_read((nel/stride)*stride+1:nel)
     ijbf = 0
     do ibf=1,basis%nbf
       do jbf=1,ibf
         ijbf = ijbf + 1
         p_matrix_out(ibf,jbf,ispin) = p_matrix_read(ijbf)
         p_matrix_out(jbf,ibf,ispin) = p_matrix_read(ijbf)
       enddo
     enddo
   enddo
   close(fu)


   ! Reorder the basis functions from Gaussian to Libint convention
   ! s and p orbitals are unchanged
   ! gaussian d orbital order is xx, yy, zz, xy, yz, xz
   ! libint   d orbital order is xx, xy, xz, yy, yz, zz

   block_d(:,:) = RESHAPE( [ 1, 0, 0, 0, 0, 0, &
                             0, 0, 0, 1, 0, 0, &
                             0, 0, 0, 0, 1, 0, &
                             0, 1, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 1, &
                             0, 0, 1, 0, 0, 0 ] , [ 6, 6 ] )

   block_f(:,:) = RESHAPE( [ 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, &    !OK
                             0, 0, 0, 0, 1, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 1, 0, 0, 0, 0, &
                             0, 0, 0, 1, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 1, &
                             0, 0, 0, 0, 0, 0, 1, 0, 0, 0, &
                             0, 1, 0, 0, 0, 0, 0, 0, 0, 0, &    !OK
                             0, 0, 0, 0, 0, 0, 0, 0, 1, 0, &
                             0, 0, 0, 0, 0, 0, 0, 1, 0, 0, &
                             0, 0, 1, 0, 0, 0, 0, 0, 0, 0 ] , [ 10, 10 ] )    !OK

   block_g(:,:) = RESHAPE( [ 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ] , [ 15, 15 ] )

   block_h(:,:) = RESHAPE( [ &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ] , [ 21, 21 ] )

   block_j(:,:) = RESHAPE( [ &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, &
                             1, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0 ] , [ 28, 28 ] )

   swap(:,:) = 0.0_dp
   ibf = 1
   do while( ibf <= basis%nbf )
     select case(basis%bfc(ibf)%am)
     case(0)
       swap(ibf,ibf) = 1.0_dp
     case(1)
       swap(ibf  ,ibf  ) = 1.0_dp
       swap(ibf+1,ibf+1) = 1.0_dp
       swap(ibf+2,ibf+2) = 1.0_dp
     case(2)
       swap(ibf:ibf+5,ibf:ibf+5) = block_d(:,:)
     case(3)
       swap(ibf:ibf+9,ibf:ibf+9) = block_f(:,:)
     case(4)
       swap(ibf:ibf+14,ibf:ibf+14) = block_g(:,:)
     case(5)
       swap(ibf:ibf+20,ibf:ibf+20) = block_h(:,:)
     case(6)
       swap(ibf:ibf+27,ibf:ibf+27) = block_j(:,:)
     case default
       call die('read_gaussian_fchk: too high angular momentum, not coded yet')
     end select
     ibf = ibf + number_basis_function_am('CART',basis%bfc(ibf)%am)
   enddo

   p_matrix_out(:,:,1) = MATMUL( TRANSPOSE(swap), MATMUL(p_matrix_out(:,:,1),swap) )


   !call dump_out_matrix(.TRUE.,'gaussian density matrix',SIZE(p_matrix_out,DIM=1),SIZE(p_matrix_out,DIM=3),p_matrix_out)


   deallocate(p_matrix_read)

 endif

 ! Broadcast the density matrix from proc iomaster to all the other procs.
 call xbcast_world(iomaster,p_matrix_out)


end subroutine read_gaussian_fchk


!=========================================================================
subroutine write_energy_qp(nstate,energy_qp)
 implicit none

 integer,intent(in)  :: nstate
 real(dp),intent(in) :: energy_qp(nstate,nspin)
!=====
 integer           :: energy_qpfile
 integer           :: istate
!=====

 !
 ! Only the proc iomaster writes down the ENERGY_QP file
 if( .NOT. is_iomaster) return

 write(stdout,'(/,a)') ' Writing ENERGY_QP file'


 open(newunit=energy_qpfile,file='ENERGY_QP',form='formatted')

 write(energy_qpfile,*) nspin
 write(energy_qpfile,*) nstate
 select case(nspin)
 case(1)
   do istate=1,nstate
     write(energy_qpfile,*) istate,energy_qp(istate,1)
   enddo
 case(2)
   do istate=1,nstate
     write(energy_qpfile,*) istate,energy_qp(istate,1),energy_qp(istate,2)
   enddo
 end select

 close(energy_qpfile)


end subroutine write_energy_qp


!=========================================================================
subroutine read_energy_qp(nstate,energy_qp,reading_status)
 implicit none

 integer,intent(in)   :: nstate
 integer,intent(out)  :: reading_status
 real(dp),intent(out) :: energy_qp(nstate,nspin)
!=====
 integer           :: energy_qpfile
 integer           :: istate,jstate
 integer           :: nspin_read,nstate_read
 logical           :: file_exists_capitalized,file_exists
!=====

 write(stdout,'(/,a)') ' Reading ENERGY_QP file'

 inquire(file='ENERGY_QP',exist=file_exists_capitalized)
 inquire(file='energy_qp',exist=file_exists)

 if(file_exists_capitalized) then
   open(newunit=energy_qpfile,file='ENERGY_QP',form='formatted',status='old')
 else if(file_exists) then
   open(newunit=energy_qpfile,file='energy_qp',form='formatted',status='old')
 endif

 if( file_exists_capitalized .OR. file_exists ) then
   read(energy_qpfile,*) nspin_read
   read(energy_qpfile,*) nstate_read
   if( nstate_read /= nstate .OR. nspin_read /= nspin ) then
     call issue_warning('ENERGY_QP file does not have the correct dimensions')
     reading_status=2
   else
     do istate=1,nstate
       read(energy_qpfile,*) jstate,energy_qp(istate,:)
       ! Scissor operator
       if( jstate == -1 ) then
         reading_status=-1
         close(energy_qpfile)
         return
       endif
     enddo
     reading_status=0
   endif
   close(energy_qpfile)
 else
   reading_status=1
   call issue_warning('files ENERGY_QP and energy_qp do not exist')
 endif


end subroutine read_energy_qp


!=========================================================================
subroutine evaluate_wfn_r(nspin,nstate,basis,c_matrix,istate1,istate2,ispin,rr,wfn_i)
 implicit none
 integer,intent(in)         :: nspin
 type(basis_set),intent(in) :: basis
 integer,intent(in)         :: nstate
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
 integer,intent(in)         :: istate1,istate2,ispin
 real(dp),intent(in)        :: rr(3)
 real(dp),intent(out)       :: wfn_i(istate1:istate2)
!=====
 real(dp)                   :: basis_function_r(basis%nbf)
!=====

 ! First precalculate all the needed basis function evaluations at point rr
 call calculate_basis_functions_r(basis,rr,basis_function_r)

 ! Then rotate
 wfn_i(istate1:istate2) = MATMUL( basis_function_r(:) , c_matrix(:,istate1:istate2,ispin) )


end subroutine evaluate_wfn_r


!=========================================================================
function wfn_parity(nstate,basis,c_matrix,istate,ispin)
 implicit none
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
 integer,intent(in)         :: istate,ispin
 integer                    :: wfn_parity
!=====
 real(dp) :: phi_tmp1(1),phi_tmp2(1),xtmp(3)
!=====

 xtmp(1) = xcenter(1) +  2.0_dp
 xtmp(2) = xcenter(2) +  1.0_dp
 xtmp(3) = xcenter(3) +  3.0_dp
 call evaluate_wfn_r(nspin,nstate,basis,c_matrix,istate,istate,ispin,xtmp,phi_tmp1)
 xtmp(1) = xcenter(1) -  2.0_dp
 xtmp(2) = xcenter(2) -  1.0_dp
 xtmp(3) = xcenter(3) -  3.0_dp
 call evaluate_wfn_r(nspin,nstate,basis,c_matrix,istate,istate,ispin,xtmp,phi_tmp2)

 if( ABS(phi_tmp1(1) - phi_tmp2(1))/ABS(phi_tmp1(1)) < 1.0e-6_dp ) then
   wfn_parity = 1
 else
   wfn_parity = -1
 endif


end function wfn_parity


!=========================================================================
function wfn_reflection(nstate,basis,c_matrix,istate,ispin)
 implicit none
 integer,intent(in)         :: nstate
 type(basis_set),intent(in) :: basis
 real(dp),intent(in)        :: c_matrix(basis%nbf,nstate,nspin)
 integer,intent(in)         :: istate,ispin
 integer                    :: wfn_reflection
!=====
 real(dp) :: phi_tmp1(1),phi_tmp2(1)
 real(dp) :: xtmp1(3),xtmp2(3)
 real(dp) :: proj
!=====

 xtmp1(1) = xatom(1,1) +  2.0_dp
 xtmp1(2) = xatom(2,1) +  1.0_dp
 xtmp1(3) = xatom(3,1) +  3.0_dp
 call evaluate_wfn_r(nspin,nstate,basis,c_matrix,istate,istate,ispin,xtmp1,phi_tmp1)

 proj = DOT_PRODUCT( xtmp1 , xnormal )
 xtmp2(:) = xtmp1(:) -  2.0_dp * proj * xnormal(:)
 call evaluate_wfn_r(nspin,nstate,basis,c_matrix,istate,istate,ispin,xtmp2,phi_tmp2)

 if( ABS(phi_tmp1(1) - phi_tmp2(1))/ABS(phi_tmp1(1)) < 1.0e-6_dp ) then
   wfn_reflection = 1
 else if( ABS(phi_tmp1(1) + phi_tmp2(1))/ABS(phi_tmp1(1)) < 1.0e-6_dp ) then
   wfn_reflection = -1
 else
   wfn_reflection = 0
 endif


end function wfn_reflection


!=======================================
subroutine print_2d_matrix_cmplx(desc,matrix_cmplx,size_n,size_m,prec,beg)
 implicit none
 integer, intent(in)      :: prec ! precision
 integer, intent(in)      :: beg  ! number of characters in the beginning
 integer, intent(in)      :: size_n,size_m
 complex(dp),intent(in)   :: matrix_cmplx(size_n,size_m)
 character(*),intent(in)  :: desc
!=====
 character(100)  :: write_format1, write_format2
 integer            :: ivar
!=====

 write(write_format1,*) '(',size_m," ('( ',F", prec+beg, ".", prec,"' ,',F", prec+beg, ".",prec,",' )  ') " ,')' ! (  1.01 ,  -0.03)  (  0.04 ,  0.10)
 write(write_format2,*) '(',size_m," (F", prec+beg, ".", prec,"' +  i',F", prec+beg, ".",prec,",'  ') " ,')'   ! 1.01 +  i  -0.03    0.03 +  i  0.10
 write(stdout,*) desc
 do ivar=1,size_n
   write(stdout,write_format1) matrix_cmplx(ivar,:)
 end do

end subroutine print_2d_matrix_cmplx


!=======================================
subroutine print_2d_matrix_real(desc,matrix_real,size_n,size_m,prec,beg)
 implicit none
 integer, intent(in)      :: prec ! precision
 integer, intent(in)      :: beg  ! number of characters in the beginning
 integer, intent(in)      :: size_n,size_m
 real(dp),intent(in)      :: matrix_real(size_m,size_m)
 character(*),intent(in)  :: desc
!=====
 character(100)  :: write_format1
 integer            :: ivar

 write(write_format1,*) '(',size_m," (F", prec+beg, ".", prec,') ' ,')'
 write(stdout,*) desc
 do ivar=1,size_n
   write(stdout,write_format1) matrix_real(ivar,:)
 end do
end subroutine print_2d_matrix_real


!=========================================================================
subroutine calculation_parameters_yaml(nbf,auxil_nbf,nstate)
 implicit none
 integer,intent(in)  :: nbf,auxil_nbf,nstate
!=====

 if( .NOT. ( print_yaml_ .AND. is_iomaster ) ) return

 write(unit_yaml,'(/,a)')  'calculation parameters:'
 write(unit_yaml,'(4x,a,1x,i6)')  'basis functions:             ',nbf
 write(unit_yaml,'(4x,a,1x,i6)')  'independent basis functions: ',nstate
 write(unit_yaml,'(4x,a,1x,i6)')  'auxiliary basis functions:   ',auxil_nbf

end subroutine calculation_parameters_yaml

!=========================================================================
subroutine evaluate_memory(nbf,auxil_nbf,nstate,occupation)
 implicit none
 integer,intent(in)  :: nbf,auxil_nbf,nstate
 real(dp),intent(in) :: occupation(:,:)
!=====
 integer :: ncore_W,nvirtual_W,nhomo
 real(dp) :: mem
!=====

 write(stdout,'(/,1x,70("="))')

 write(stdout,'(/,1x,a)') 'Memory consumption predictor'
 write(stdout,'(1x,a,/)') 'The above numbers are just for information purpose. Handle with care.'

 write(stdout,'(/,1x,a)') '==== Electron Repulsion Integrals (MPI distributed)'
 write(stdout,'(5x,a30,1x,i10)') 'npair =',npair
 if( has_auxil_basis ) then
   write(stdout,'(5x,a30,1x,i10)') 'auxil_basis%nbf =',auxil_nbf
   write(stdout,'(5x,a30,1x,i10)') 'eri3_nbatch =',eri3_nbatch
   !                                        eri_3center +       eri_3center_tmp
   mem = REAL(npair,dp) * REAL(auxil_nbf,dp) * ( 1.0_dp + 1.0_dp / REAL(eri3_nbatch,dp) )
 else
   !          eri_4center    / symmetry
   mem = REAL(npair,dp)**2   / 2.0_dp
 endif
 mem = mem * 8.0_dp / 1024_dp**3
 write(stdout,'(5x,a30,1x,f14.3,1x,a)') 'Memory =',mem,'(Gb)'


 write(stdout,'(/,1x,a)')  '==== Pulay DIIS (never distributed)'
 write(stdout,'(5x,a30,1x,i10)') 'basis%nbf =',nbf
 write(stdout,'(5x,a30,1x,i10)') 'nstate =',nstate
 write(stdout,'(5x,a30,1x,i10)') 'nspin =',nspin
 write(stdout,'(5x,a30,1x,i10)') 'npulay_hist =',npulay_hist
 !                                        ham_hist & p_matrix_hist      res_hist
 mem = REAL(npulay_hist * nspin,dp) * (  2.0_dp * REAL(nbf,dp)**2    +  REAL(nstate,dp)**2 )
 mem = mem * 8.0_dp / 1024_dp**3
 write(stdout,'(5x,a30,1x,f14.3,1x,a)') 'Memory =',mem,'(Gb)'


 write(stdout,'(/,1x,a)')  '==== RPA response calculation (MPI distributed)'
 ncore_W      = ncorew
 nvirtual_W   = MIN(nvirtualw,nstate+1)
 if(is_frozencore) then
   if( ncore_W == 0) ncore_W = atoms_core_states()
 endif
 nhomo = get_number_occupied_states(occupation)

 write(stdout,'(5x,a)')  '== RPA response'
 write(stdout,'(5x,a30,1x,i10)') 'Active occupied states =',nhomo-ncore_W
 write(stdout,'(5x,a30,1x,i10)') 'Active virtual states =',nvirtual_W-nhomo-1
 write(stdout,'(5x,a30,1x,i10)') 'nspin =',nspin
 !                                                                            A+B , Z
 mem = ( REAL(nhomo-ncore_W,dp) * REAL(nvirtual_W-nhomo-1,dp) * nspin )**2 * 2.0_dp
 mem = mem * 8.0_dp / 1024_dp**3
 write(stdout,'(5x,a30,1x,f14.3,1x,a)') 'Memory =',mem,'(Gb)'

 write(stdout,'(5x,a)')  '== MO Electron Repulsion Integrals'
 write(stdout,'(5x,a30,1x,i10)') 'Active occupied states =',nhomo-ncore_W
 write(stdout,'(5x,a30,1x,i10)') 'Active virtual states =',nvirtual_W-nhomo-1
 if( has_auxil_basis ) then
   write(stdout,'(5x,a30,1x,i10)') 'auxil_basis%nbf =',auxil_nbf
   !                                                              eri_3center_ao
   mem = REAL(nhomo-ncore_W,dp) * REAL(nvirtual_W-nhomo-1,dp) * nspin * auxil_nbf
 else
   !          eri_eigenstate_klmin(
   mem = REAL(nbf,dp)**3 * nspin
 endif
 mem = mem * 8.0_dp / 1024_dp**3
 write(stdout,'(5x,a30,1x,f14.3,1x,a)') 'Memory =',mem,'(Gb)'


 write(stdout,'(/,1x,a)')  '==== RT-TDDFT (never distributed)'
 write(stdout,'(5x,a30,1x,i10)') 'nstate =',nstate
 write(stdout,'(5x,a30,1x,i10)') 'nspin =',nspin
 write(stdout,'(5x,a30,1x,i10)') 'n_hist =',n_hist
 !               h_small_hist_cmplx(:,:,:,:)  h_small_cmplx          h_cmplx
 mem = REAL(nstate,dp)**2 * nspin * ( n_hist  +    1            ) + REAL(nbf,dp)**2 * nspin
 mem = mem * 16.0_dp / 1024_dp**3
 write(stdout,'(5x,a30,1x,f14.3,1x,a)') 'Memory =',mem,'(Gb)'



 write(stdout,'(/,1x,70("="))')


 call stop_clock(timing_prescf)
 call stop_clock(timing_total)
 call this_is_the_end()

end subroutine evaluate_memory


!=========================================================================
end module m_io
!=========================================================================
